;MEMORY: ENABLES REQUESTED 32K MEMORY BLOCK.
; Assembled OK 30-Mar-85.

*GET	DOSCALLS

	ORG	5200H
MEMORY	CALL	BCMD
	JR	Z,PRINT
	CP	'1'
	JP	Z,SET1
	CP	'0'
	JP	Z,SET0
PRINT	LD	A,(403DH)
	AND	20H
	JR	NZ,ONE
	LD	HL,ZMESS
MESS	CALL	MESS_DO
	LD	HL,MESS2
	CALL	MESS_DO
	LD	A,(4BDAH)
	CP	21H
	JR	Z,PAST1
	LD	HL,MESS4
	CALL	MESS_DO
	LD	A,(4BDAH)
	CP	195
	JR	Z,PAST05
	LD	A,'2'
LABEL	CALL	ROM@PUT_VDU
	LD	HL,MESS5
	JR	PRDOS
PAST05	LD	A,'1'
	JR	LABEL
PAST1	LD	HL,MESS3
PRDOS	CALL	MESS_DO
	LD	HL,HIMESS
	CALL	MESS_DO
	LD	HL,(4049H)
	LD	A,H
	CALL	HEX01
	LD	A,L
	CALL	HEX01
	LD	A,'H'
	CALL	ROM@PUT_VDU
	LD	A,0DH
	CALL	ROM@PUT_VDU
	JP	DOS_NOERROR
HEX01	LD	E,A
	AND	0F0H
	SRL	A
	SRL	A
	SRL	A
	SRL	A
	CALL	HEX02
	LD	A,E
	AND	0FH
	CALL	HEX02
	RET
HEX02	ADD	A,30H
	CP	3AH
	JR	C,HEXV01
	ADD	A,7
HEXV01	PUSH	HL
	PUSH	DE
	CALL	ROM@PUT_VDU
	POP	DE
	POP	HL
	RET
ONE	LD	HL,OMESS
	JR	MESS
SET1	LD	A,(403DH)
	OR	20H
SET	LD	(403DH),A
	OUT	(255),A
	JP	PRINT
SET0	LD	A,(403DH)
	AND	0DFH
	JR	SET
ZMESS	DEFM	'MEMORY ZERO'
	DEFB	0DH
	DEFB	00H
OMESS	DEFM	'MEMORY ONE'
	DEFB	0DH
	DEFB	00H
MESS2	DEFM	'SYSTEM LOADER IS '
	DEFB	03H
MESS3	DEFM	'NOT INSTALLED.'
	DEFB	0DH
MESS4	DEFM	'INSTALLED (TYPE '
	DEFB	03H
MESS5	DEFM	').'
	DEFB	0DH
HIMESS	DEFM	'CURRENT HIGH MEMORY VALUE IS '
	DEFB	03H
BCMD	LD	HL,4318H
BCMV01	LD	A,(HL)
	CP	20H
	JR	Z,BCSP
	CP	0DH
	RET	Z
	INC	HL
	JR	BCMV01
BCSP	LD	A,(HL)
	INC	HL
	CP	20H
	JR	Z,BCSP
	DEC	HL
	CP	0DH
	RET
	END	MEMORY
