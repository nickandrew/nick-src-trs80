; wavelang:1
; 19-Dec-84.
;
EXEC_COMM
	LD	HL,(WAVE_DELAY)
	LD	A,H
	OR	L
	JR	Z,EC_1
	DEC	HL
	LD	(WAVE_DELAY),HL
	RET
EC_1	LD	HL,(WAVE_PC)
	LD	A,(HL)
	CP	12
	RET	NC
	ADD	A,A
	PUSH	HL
	LD	E,A
	LD	D,0
	LD	HL,W_TABLE
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	POP	HL
	PUSH	DE
	POP	IX
	JP	(IX)
;
W_TABLE	DEFW	W_REST,W_WAVE,W_FINISH,W_NEWMAN
	DEFW	W_WAIT,W_KILLALL,W_NEW,W_WREPT
	DEFW	W_WENDR,W_STOP,W_JMP,W_SYNC
;
W_REST	INC	HL
	LD	(WAVE_PC),HL
	RET
;
W_WAVE	LD	(WAVE_START),HL
	INC	HL
	LD	(WAVE_PC),HL
	CALL	CLS
	LD	A,1
	LD	(ALIEN_NO),A
	LD	A,(WAVE_CTR)
	INC	A
	LD	(WAVE_CTR),A
	LD	HL,WAVE_MESS
	LD	DE,7*64+3C00H+25
	LD	BC,5
	LDIR
	EX	DE,HL
	LD	A,(WAVE_CTR)
	CALL	BYTE_DEC
	LD	BC,0
	CALL	60H
	CALL	60H
	CALL	60H
	CALL	NOSHOTS
;	LD	HL,LOOP_STK
;	LD	(LOOP_PTR),HL
	CALL	CLS
	LD	HL,S_VDU
	LD	DE,S_VDU+1
	LD	(HL),80H
	LD	BC,03FFH
	LDIR
	LD	A,START_POSN
	LD	(MAN_POSN),A
	XOR	A
	LD	(TOP_ALIEN),A
	RET
;
WAVE_MESS
	DEFM	'Wave '
;
W_FINISH
	LD	A,(NUM_ALIEN)
	OR	A
	RET	NZ
	INC	HL
	LD	(WAVE_PC),HL
	RET
;
W_NEWMAN
	INC	HL
	LD	(WAVE_PC),HL
	LD	A,(MEN_LEFT)
	INC	A
	LD	(MEN_LEFT),A
	RET
;
W_WAIT	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(WAVE_PC),HL
	DEC	DE
	EX	DE,HL
	LD	(WAVE_DELAY),HL
	RET
;
W_KILLALL
	INC	HL
	LD	(WAVE_PC),HL
	LD	HL,AL_TAB
	LD	B,MAX_ALIEN
	LD	DE,5
WK_1	LD	(HL),0
	ADD	HL,DE
	DJNZ	WK_1
	XOR	A
	LD	(NUM_ALIEN),A
	LD	(TOP_ALIEN),A
;	LD	HL,LOOP_STK
;	LD	(LOOP_PTR),HL
	RET
;
W_NEW	LD	DE,5
	PUSH	HL
	ADD	HL,DE
	LD	(WAVE_PC),HL
	LD	A,(NUM_ALIEN)
	CP	MAX_ALIEN
	POP	HL
	JP	NC,EXEC_COMM
	PUSH	HL
	LD	HL,AL_TAB
	LD	B,MAX_ALIEN
	LD	C,0
	LD	DE,5
WN_1	LD	A,(HL)
	AND	1
	JR	Z,WN_2
	ADD	HL,DE
	INC	C
	DJNZ	WN_1
	JP	ERROR
WN_2	LD	(HL),1
	LD	A,(TOP_ALIEN)
	LD	B,A
	LD	A,C
	CP	B
	JR	C,WN_21
	LD	A,C
	INC	A
	LD	(TOP_ALIEN),A
WN_21	INC	HL
	POP	DE
	INC	DE
	LD	BC,4
	EX	DE,HL
	LDIR
	LD	A,(NUM_ALIEN)
	INC	A
	LD	(NUM_ALIEN),A
	JP	EXEC_COMM
;
W_WREPT	INC	HL
	LD	A,(HL)
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(WAVE_PC),HL
	JP	EXEC_COMM
;
W_WENDR
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(WAVE_PC),HL
	INC	DE
	INC	DE
	EX	DE,HL
	DEC	(HL)
	JP	Z,EXEC_COMM
	INC	HL
	LD	(WAVE_PC),HL
	JP	EXEC_COMM
;
W_STOP	LD	HL,MSG1
	CALL	4467H
	JP	402DH
;
MSG1	DEFM	'Bye!',0DH
;
W_JMP	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	LD	(WAVE_PC),HL
	JP	EXEC_COMM
;
W_SYNC	INC	HL
	LD	A,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(WAVE_PC),HL
	CALL	SYNCHRON
	RET
;
