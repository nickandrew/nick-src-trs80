;control: 'Marooned' controller program.
; Assembled OK 30-Mar-85.

*GET	DOSCALLS

	ORG	8000H
ROOMIN	EQU	32767
INVENT	EQU	32760
CLS	EQU	01C9H
BASIC	EQU	402DH
VERBT	EQU	32744
COMDOT	EQU	32746
SPEDOT	EQU	32748
SPGOT	EQU	32750
OBTABL	EQU	32752
OBROOM	EQU	32754
RMNAME	EQU	32756
BEFORE	EQU	32758
START	DI
	LD	HL,40A7H
	LD	(HL),232
	INC	HL
	LD	(HL),65
	LD	SP,32700
	LD	HL,BASIC
	PUSH	HL
	LD	HL,(BEFORE)
	PUSH	HL
	POP	DE
	INC	DE
	LD	BC,255
	LD	(HL),0
	LDIR
	LD	HL,AZZZ
	LD	(32742),HL
	LD	A,1
	LD	(ROOMIN),A
	CALL	CLS
	LD	HL,INTRO
	CALL	VDUOUT
AZZZ	CALL	SKPLIN
	LD	D,0
	LD	A,(ROOMIN)
	LD	E,A
	LD	HL,(BEFORE)
	ADD	HL,DE
	LD	A,(HL)
	OR	A
	PUSH	AF
	LD	(HL),255
	LD	HL,(RMNAME)
AAAAA	LD	A,(HL)
	CP	255
	JR	Z,AAAAB
	CP	0
	JP	Z,ERROR
	INC	HL
	JR	AAAAA
AAAAB	INC	HL
	LD	A,(ROOMIN)
	CP	(HL)
	JR	NZ,AAAAA
	INC	HL
	POP	AF
	JR	Z,AAAAC
AAAAD	LD	A,(HL)
	CP	'^'
	INC	HL
	JR	NZ,AAAAD
AAAAC	CALL	VDUOUT
	CALL	SKPLIN
	LD	HL,(OBROOM)
AAAAE	LD	A,(HL)
	CP	0
	JP	Z,AAAAF
AAAAG	CP	255
	JR	Z,AAAAH
	INC	HL
	LD	A,(HL)
	JR	AAAAG
AAAAH	INC	HL
	INC	HL
	LD	A,(ROOMIN)
	CP	(HL)
	JR	Z,AAAAI
	INC	HL
	JR	AAAAE
AAAAI	DEC	HL
	LD	A,(HL)
	INC	HL
	INC	HL
	PUSH	HL
	CALL	DISPOB
	POP	HL
	JR	AAAAE
DISPOB	LD	HL,(OBTABL)
	LD	D,A
AAAAJ	LD	A,(HL)
	OR	A
	JR	Z,ERROR
AAAAK	CP	255
	JR	Z,AAAAL
	INC	HL
	LD	A,(HL)
	JR	AAAAJ
AAAAL	INC	HL
	LD	A,D
	CP	(HL)
	JR	Z,AAAAM
	INC	HL
	JR	AAAAJ
AAAAM	INC	HL
	CALL	VDUOUT
	LD	A,0DH
	CALL	ROM@PUT_VDU
	RET
VDUOUT	LD	A,(HL)
	CP	'^'
	RET	Z
	CALL	ROM@PUT_VDU
	INC	HL
	LD	BC,512
	CALL	0060H
	JR	VDUOUT
SKPLIN	LD	A,0DH
	CALL	ROM@PUT_VDU
	RET
INTRO	DEFM	'WELCOME TO THE TRILOBYTE ADVENTURE.'
	DEFB	0DH
	DEFB	'^'
NOWAY	DEFM	'THERE IS NO WAY TO GO THAT DIRECTION.^'
ERROR	LD	HL,ERRORM
	CALL	VDUOUT
	JP	BASIC
ERRORM	DEFM	'ERROR FOUND IN TABLE ACCESS...'
	DEFB	0DH
	DEFB	'^'
NOGO	LD	A,R
	POP	IX
	POP	IX
	LD	HL,AZZZ
	PUSH	HL
	AND	3
	CP	0
	JR	NZ,D0
	LD	HL,MES0
	JP	VDUOUT
D0	CP	1
	JR	NZ,D1
	LD	HL,MES1
	JP	VDUOUT
D1	CP	2
	JR	NZ,D2
	LD	HL,MES2
	JP	VDUOUT
D2	LD	HL,MES3
	JP	VDUOUT
MES0	DEFM	'I DON'
	DEFB	39
	DEFM	'T UNDERSTAND THAT.^'
MES1	DEFM	'PARDON ME??^'
MES2	DEFM	'SORRY,	CAN'
	DEFB	39
	DEFM	'T DO THAT.^'
MES3	DEFM	'I THINK YOU SPEAK GIBBERISH.^'
;####### WONDERFUL PLACE TO PUT SUBROUTINES....
AAAAF	CALL	SKPLIN
	CALL	INPUT	;ENCOMPASSES A LOT
; INPUT: OUTPUT VERB,OBJECT INTO D,E
; SEARCH SPGOT (SPECIAL GO TABLE)
	LD	HL,(SPGOT)
ABBA	LD	A,(HL)
	OR	A
	JR	Z,ABBE
	CP	255
	JR	Z,ABBC
	CP	D
	JR	NZ,ABBB
	INC	HL
	LD	A,(HL)
	OR	A
	JR	NZ,ABBF
	LD	HL,NOWAY
	CALL	VDUOUT
	JP	AZZZ
ABBF	LD	(ROOMIN),A
	JP	AZZZ
ABBB	INC	HL
	INC	HL
	JR	ABBA
ABBC	INC	HL
	LD	A,(ROOMIN)
	CP	(HL)
	JR	NZ,ABBD
	INC	HL
	JR	ABBA
ABBD	INC	HL
ABBG	INC	HL
	INC	HL
	LD	A,(HL)
	OR	A
	JR	Z,ABBE
	CP	255
	JR	NZ,ABBG
	JR	ABBC
; NEXT: CONSULT SPEDOT, (SPECIAL DO TABLE)...
ABBE	LD	HL,(SPEDOT)
ACBA	LD	A,(HL)
	OR	A
	JR	Z,AAAAT
	INC	HL
	LD	A,(ROOMIN)
	CP	(HL)
	JR	Z,ACBC
	INC	HL
ACBB	INC	HL
	INC	HL
	INC	HL
	JR	ACBA
ACBC	INC	HL
	LD	A,(HL)
	CP	D
	JR	NZ,ACBB
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	PUSH	BC
	POP	HL
	JP	(HL)
; NEXT CONSULT COMDOT
AAAAT	LD	HL,(COMDOT)
AAAAW	LD	A,(HL)
	OR	A
	JP	Z,ERROR
	CP	D
	JR	Z,AAAAX
	INC	HL
	INC	HL
	INC	HL
	JR	AAAAW
AAAAX	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	PUSH	BC
	POP	HL
	JP	(HL)
;INPUT SUBROUTINE...
INPUT	XOR	A
	LD	HL,(40A7H)
	LD	B,63
	CALL	05D9H
	LD	HL,(40A7H)
	PUSH	HL
	POP	DE
; FOLLOWING CODE WILL 'TOKENISE' VERB...
	LD	HL,(VERBT)
BA	PUSH	DE
	LD	A,(HL)
	OR	A
	JP	Z,NOGO
	CP	255
	JR	NZ,BB
	INC	HL
	INC	HL
BB	LD	A,(HL)
	CP	'^'
	JR	NZ,BE
	LD	A,(DE)
	CP	32
	JR	Z,BI
	CP	0DH
	JR	NZ,BF
BI	DEC	HL
	LD	A,(HL)
	CP	255
	JR	NZ,BI
	INC	HL
	LD	B,(HL)
	JR	OBJ2
BD	LD	A,(HL)
	CP	'^'
	JR	NZ,BF
	JR	BI
BE	LD	A,(DE)
	CP	0DH
	JR	Z,BD
	CP	32
	JR	Z,BD
	CP	(HL)
	JR	Z,BH
BF	POP	DE
BG	LD	A,(HL)
	INC	HL
	CP	'^'
	JR	NZ,BG
	JR	BA
BH	INC	HL
	INC	DE
	JR	BB
;FOLLOWING: OBJECT NUMBER DECODER ROUTINE
OBJ2	LD	HL,(OBTABL)
	POP	DE
ACC	LD	A,(DE)
	CP	0DH
	JR	NZ,ACA
	LD	C,0
AEA	PUSH	BC
	POP	DE
	RET
ACA	CP	32
	JR	Z,AZA
	INC	DE
	JR	ACC
AZA	INC	DE
ABA	PUSH	DE
	LD	A,(HL)
	OR	A
	JP	Z,NOGO
	CP	255
	JR	NZ,ABB
	INC	HL
	INC	HL
ADA	LD	A,(HL)
	INC	HL
	CP	'^'
	JR	NZ,ADA
ADB	LD	A,(HL)
	INC	HL
	CP	'^'
	JR	NZ,ADB
ABB	LD	A,(HL)
	CP	'^'
	JR	NZ,ABE
	LD	A,(DE)
	CP	32
	JR	Z,ABI
	CP	0DH
	JR	NZ,ABF
ABI	DEC	HL
	LD	A,(HL)
	CP	255
	JR	NZ,ABI
	INC	HL
	LD	C,(HL)
	POP	IX
	JR	AEA
ABD	LD	A,(HL)
	CP	'^'
	JR	NZ,ABF
	JR	ABI
ABE	LD	A,(DE)
	CP	0DH
	JR	Z,ABD
	CP	32
	JR	Z,ABD
	CP	(HL)
	JR	Z,ABH
ABF	POP	DE
ABG	LD	A,(HL)
	INC	HL
	CP	'^'
	JR	NZ,ABG
	JR	ABA
ABH	INC	HL
	INC	DE
	JR	ABB
	END	START
