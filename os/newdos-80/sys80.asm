;SYS80: LOADS IN VARIOUS SYSTEM FILES
;TO MEMORY THEN ACTS AS SYSLOAD.
;Files are taken from extra 32K bank-switched ram.
; Assembled OK 30-Mar-85.
ORIG	EQU	0FB00H
	ORG	ORIG
SYSRES	LD	HL,4318H
	CALL	NEXTWORD
	PUSH	HL
	LD	HL,TABLE
	PUSH	HL
	POP	DE
	INC	DE
	LD	(HL),0
	LD	BC,255
	LDIR
	POP	HL
RDSYS	LD	A,(HL)
	CP	0DH
	JP	NZ,NODOS
	LD	HL,(CRLOAD)
	CALL	SETUP
	LD	HL,MESS1
	CALL	MESS$DI
	LD	IX,402DH
	LD	A,(403DH)
	AND	0DFH
	LD	(403DH),A
	JP	4BE1H
SETUP	LD	HL,REPCOD
	LD	DE,4BDAH
	LD	BC,14
	LDIR
	RET
NODOS	CALL	GETNUM
	CALL	READSYS
	JR	RDSYS
NEXTWORD LD	A,(HL)
	INC	HL
	CP	21H
	JR	NC,NEXTWORD
	DEC	HL
NEXV01	LD	A,(HL)
	INC	HL
	CP	20H
	JR	Z,NEXV01
	DEC	HL
	RET
GETNUM	LD	(CST),HL
	PUSH	HL
	POP	BC
	LD	HL,0
GETV01	CALL	GETCH
	JR	Z,GPAST
	LD	A,L
	LD	(CURSYS),A
	PUSH	BC
	POP	HL
	LD	(CEN),HL
	RET
GPAST	PUSH	HL
	POP	DE
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,DE
	ADD	HL,HL
	ADD	A,L
	LD	L,A
	JR	GETV01
GETCH	LD	A,(BC)
	INC	BC
	CP	0DH
	JR	Z,CHEND
	CP	20H
	JR	NZ,GCHV01
GCHL01	LD	A,(BC)
	INC	BC
	CP	20H
	JR	Z,GCHL01
CHEND	LD	A,0FFH
	DEC	BC
	OR	A
	RET
GCHV01	CP	3AH
	JR	NC,GETCH
	CP	30H
	JR	C,GETCH
	SUB	30H
	LD	D,A
	XOR	A
	LD	A,D
	RET
READSYS	LD	A,(CURSYS)
	LD	HL,(CRTPS)
	LD	(HL),A
	INC	HL
	PUSH	HL
	LD	HL,(CRLOAD)
	EX	DE,HL
	POP	HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
	LD	(CRTPS),HL
	LD	HL,(CST)
	LD	DE,BUFF1
MV01	LD	A,(HL)
	CP	20H
	JR	Z,MV02
	LD	(DE),A
	INC	HL
	INC	DE
	JR	MV01
MV02	LD	A,0DH
	LD	(DE),A
	LD	HL,BUFF1
	LD	DE,FCB
	CALL	EXTRACT
	LD	HL,DEFEXT
	LD	DE,FCB
	CALL	EXTEND
	LD	HL,BUFF1
	LD	DE,FCB
	LD	B,0
	CALL	OPEN$EX
	JP	NZ,DOSERR
	LD	HL,(CRLOAD)
LV01	LD	DE,FCB
	CALL	READ$RC
	JR	NZ,LV02
	LD	B,0
	CALL	POKE
	JR	LV01
LV02	CP	1DH
	JR	NZ,LV05
	LD	A,(FCB+8)
	LD	B,A
LV03	CALL	POKE
LV04	LD	(CRLOAD),HL
;	LD	DE,FCB	;WAS TO CLOSE FCB
;	CALL	CLOSE	;NOW DISCARDED: WASTES TIME.
	LD	HL,(CEN)
	RET
LV05	CP	1CH
	JP	NZ,DOSERR
	JR	LV04
POKE	LD	DE,BUFF1
POKV01	LD	A,(DE)
	LD	(HL),A
	INC	DE
	DEC	HL
	DJNZ	POKV01
	RET
SLOAD	LD	A,(403DH)
	OR	20H
	OUT	(255),A
	LD	A,C
	LD	HL,4317H
	CP	(HL)
	JP	NZ,BYV01
	LD	IX,4C19H
	LD	A,(403DH)
	JP	4BE1H
BYV01	LD	(HL),A
	LD	B,A
	DEC	B
	DEC	B
	LD	HL,TABLE
SEARCH	LD	A,(HL)
	OR	A
	JR	NZ,BYPE
	LD	A,B
	INC	A
	INC	A
	LD	HL,4317H
	AND	7
	LD	C,A
	XOR	A
	LD	(43D8H),A
	LD	IX,4BE8H
	LD	A,(403DH)
	JP	4BE1H
BYPE	CP	B
	JR	Z,MLOAD
	INC	HL
	INC	HL
	INC	HL
	JR	SEARCH
MLOAD	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
MLOV01	LD	A,(HL)
	DEC	HL
	CP	2
	JR	NZ,MLOV02
	DEC	HL
	LD	E,(HL)
	DEC	HL
	LD	D,(HL)
	EX	DE,HL
	LD	(4C1EH),HL
	LD	A,(403DH)
	LD	IX,4C19H
	JP	4BE1H
MLOV02	CP	1
	JR	Z,MLOV04
	LD	B,(HL)
	DEC	HL
MLOV03	DEC	HL
	DJNZ	MLOV03
	JR	MLOV01
MLOV04	LD	B,(HL)
	DEC	HL
	LD	E,(HL)
	DEC	HL
	LD	D,(HL)
	DEC	HL
	DEC	B
	DEC	B
MLOV05	LD	A,(HL)
	LD	(DE),A
	DEC	HL
	INC	DE
	DJNZ	MLOV05
	JR	MLOV01
MESS1	DEFM	'NOW PATCHING DOS FOR SYSTEM LOADER'
	DEFB	0AH
	DEFM	'SPECIAL BANK-SWITCHED MEMORY VERSION'
	DEFB	0AH
	DEFM	'SYSRES/80 (VER 2.10: OCTOBER 30, 1983).'
	DEFB	0DH
REPCOD	LD	C,A
	LD	A,20H
	LD	IX,SLOAD
	OUT	(255),A
	XOR	A
	JP	(IX)
	NOP
	NOP
CST	DEFW	0
CEN	DEFW	0
CRTPS	DEFW	TABLE
TABLE	EQU	ORIG-256
DEFEXT	DEFM	'SYS'
CURSYS	DEFB	0
CRLOAD	DEFW	TABLE-1
BUFF1	DEFS	256
FCB	DEFS	32
; NEWDOS/80 VERSION 2 FUNCTION ADDRESSES
COMBUFF	EQU	4318H	;ADDRESS OF COMMAND BUFFER
CURSOR	EQU	4020H	;ADDRESS OF CURSOR POSITION
DOS	EQU	402DH	;NO ERROR EXIT
DOSCOM	EQU	4405H	;ENTER DOS AND EXECUTE COMMAND
DOSERR	EQU	4409H	;DOS ERROR EXIT
DEBUG	EQU	440DH	;ENTER DEBUG
ENQUEUE	EQU	4410H	;ENQUEUE USER INTERRUPT ROUTINE
DEQUEUE	EQU	4413H	;DEQUEUE USER INTERRUPT ROUTINE
ROTATE	EQU	4416H	;KEEP DRIVES ROTATING
DOSCMD	EQU	4419H	;EXECUTE DOS COMMAND AND RETURN
EXTRACT	EQU	441CH	;EXTRACT A FILESPEC
OPEN$NW	EQU	4420H	;OPEN A NEW OR EXISTING FILE
OPEN$EX	EQU	4424H	;OPEN AN EXISTING FILE
CLOSE	EQU	4428H	;CLOSE A FILE
KILL	EQU	442CH	;KILL A FILE
LOAD	EQU	442CH	;LOAD A FILE
EXECUTE	EQU	4433H	;LOAD THEN EXECUTE A FILE
READ$RC	EQU	4436H	;READ A FILE'S RECORD
WRIT$RC	EQU	4439H	;WRITE A RECORD TO A FILE
REWIND	EQU	443FH	;REWIND FCB
POS$FCB	EQU	4442H	;POSITION FCB TO SPECIFIED RECORD
BACKSP	EQU	4445H	;BACKSPACE FCB ONE RECORD
POS$EOF	EQU	4448H	;POSITION FCB TO EOF
ALLOCAT	EQU	444BH	;ALLOCATE FILE SPACE
SELECT	EQU	445BH	;SELECT A DRIVE
MOUNT	EQU	445EH	;TEST FOR MOUNTED DISK
NAM$ENQ	EQU	4461H	;ENQUEUE *NAME ROUTINE
NAM$DEQ	EQU	4464H	;DEQUEUE *NAME ROUTINE
MESS$DI	EQU	4467H	;SEND MESSAGE TO DISPLAY
MESS$PR	EQU	446AH	;SEND MESSAGE TO PRINTER
TIME	EQU	446DH	;GET CURRENT TIME
DATE	EQU	4470H	;GET CURRENT DATE
EXTEND	EQU	4473H	;INSERT DEFAULT EXTENSION
READ$BY	EQU	0013H	;READ A BYTE FROM A FILE
WRIT$BY	EQU	001BH	;WRITE A BYTE TO A FILE
; END OF DOS ROUTINE CALL ADDRESSES.
	END	SYSRES
