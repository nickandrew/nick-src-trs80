;erase/asm: setup *ERASE to clear all memory up to
;himem.
;Version 1.0 28-Oct-84.
;
*GET	DOSCALLS
DOSERR	EQU	4409H
ENQNAME	EQU	4461H
	ORG	5200H
START	LD	HL,(HIMEM)
	LD	(OHIMEM),HL
	LD	BC,ENDCODE-STCODE
	OR	A
	SBC	HL,BC
	LD	(HIMEM),HL
	INC	HL
	PUSH	HL
	PUSH	DE
	EX	DE,HL
	LD	HL,(RELOC1)
	ADD	HL,DE
	LD	(RELOC1),HL
	POP	DE
	POP	HL
	EX	DE,HL
	LD	HL,STCODE
	LD	BC,ENDCODE-STCODE
	LDIR
	LD	DE,XFER1-STCODE
	LD	HL,(HIMEM)
	ADD	HL,DE
	INC	HL
	CALL	ENQNAME
	PUSH	AF
	LD	HL,SIGNON
	CALL	MESS_DO
	POP	AF
	JP	NZ,DOSERR
	JP	DOS_NOERROR
;
SIGNON	DEFM	'*erase: erase memory up to himem',0AH
	DEFM	'Version 1.0, 28-Oct-84.',0DH
;
STCODE	EQU	$
OHIMEM	DEFW	0
	DEFM	'*ERASE',0DH
XFER1	DEFW	0
	DEFW	0
	DEFM	'ERASE   '
	LD	HL,(HIMEM)
	XOR	A
	LD	DE,5200H
	EX	DE,HL
LOOP1	LD	(HL),0
	LD	A,H
	CP	D
	JR	NZ,LOV01
	LD	A,L
	CP	E
	JR	Z,FINIS
LOV01	INC	HL
	JR	LOOP1
FINIS	LD	HL,MESS-STCODE
RELOC1	EQU	$-2
	CALL	MESS_DO
	RET
;
MESS	DEFM	'Memory cleared.',0DH
;
ENDCODE	NOP
;
	END	START
