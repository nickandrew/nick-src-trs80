;Spooler: File spooler.
;Version 1.0 on 01-Mar-86
;
*GET	DOSCALLS
*GET	SPOOLHDR
;
	ORG	5200H
;
START	LD	DE,SPOOLER
	CALL	4410H
	XOR	A
	LD	(QUEUE),A
	LD	HL,SPOOLER-1
	LD	(HIMEM),HL
	JP	DOS
;
	ORG	0FC00H
SPOOLER	DEFW	0
	DEFB	10	;4 times per second.
DELAY	DEFB	255	;wait 6.25 secs before running.
;
	LD	HL,14
	ADD	HL,SP	;HL points to old PC
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	HL,SPOOLER
	OR	A
	SBC	HL,DE
	JR	C,NO_EXECUTE
	LD	HL,5200H	;top address.
	OR	A
	SBC	HL,DE
	JR	C,EXECUTE
	LD	HL,4000H
	OR	A
	SBC	HL,DE
	JR	NC,EXECUTE
NO_EXECUTE
	RET
;
EXECUTE:			;safe to...
	LD	A,(4308H)
	LD	(CUR_DN),A
;
	EI			;     EI !!!
	PUSH	IX
	PUSH	IY
	EXX
	EX	AF,AF'
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(PRINTING)
	OR	A
	JR	NZ,PRT_LOOP
;Not printing a file. See if any to print
	LD	A,(QUEUE)
	OR	A
	JP	Z,EXIT		;none to print.
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,QUEUE_PTR-32
	ADD	HL,DE
	LD	DE,FCB
	LD	BC,32
	LDIR
	LD	A,(QUEUE)
	DEC	A
	LD	(QUEUE),A
	LD	HL,BUFFER
	LD	DE,FCB
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,EXIT
	LD	A,1
	LD	(PRINTING),A
	LD	A,255
	LD	(DELAY),A
EXIT
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	EX	AF,AF'
	EXX
	POP	IY
	POP	IX
;
	LD	A,(CUR_DN)
	CALL	445BH
;
	DI
;
;;	LD	A,(CUR_DN)
;;	LD	(4308H),A
;;	LD	A,(CUR_DS)
;;	LD	(4309H),A
;
	RET
;
PRT_LOOP:
	LD	A,(37E8H)
	AND	0F0H
	CP	30H
	JR	NZ,EXIT
	CALL	GETFIL
	JR	NZ,END_FILE
	LD	(37E8H),A
	LD	B,0
	DJNZ	$
	JR	PRT_LOOP
;
END_FILE
	LD	A,0CH
	LD	(37E8H),A
	LD	A,0
	LD	(PRINTING),A
	JR	EXIT
;
GETFIL	LD	DE,FCB
	CALL	$GET
	RET
;
FCB	DEFS	32
BUFFER	DEFS	256
;
PRINTING	DEFB	0
CUR_DN		DEFB	0
CUR_DS		DEFB	0
;
	END	START
