;CHANGE: CONVERTS AN EDTASM /EDT TYPE FILE
; TO A /FOR FORMAT FILE.
; Assembled OK 30-Mar-85.
	ORG	5200H
CONVERT	LD	HL,4318H
	CALL	NEXTWORD
	INC	HL
	PUSH	HL
	LD	DE,BUFFER1
	CALL	EXTRACT
	POP	HL
	CALL	NEXTWORD
	INC	HL
	LD	DE,BUFFER2
	CALL	EXTRACT
	LD	DE,BUFFER2
	LD	HL,DEFEXT2
	CALL	DEF$EXT
	LD	DE,BUFFER1
	LD	HL,DEFEXT1
	CALL	DEF$EXT
	CALL	PART1
	LD	HL,BUFFER1
	CALL	4467H
	LD	A,20H
	CALL	0033H
	CALL	PART2
	LD	HL,BUFFER2
	CALL	4467H
	LD	A,'.'
	CALL	0033H
	LD	A,0DH
	CALL	0033H
	LD	DE,BUFFER1
	LD	HL,BUFFRD1
	LD	B,0
	CALL	OPEN$EXST
	JP	NZ,ERROR
	LD	DE,BUFFER2
	LD	HL,BUFFWR2
	LD	B,0
	CALL	OPEN$NEW
	JP	NZ,ERROR
	CALL	READ1
	CP	211
	LD	A,32	;'FORMAT ERROR'
	JP	NZ,ERROR
	CALL	READ1
	CP	211
	LD	A,32
	JP	Z,ERROR
	LD	B,5
READNAME	PUSH	BC
	CALL	READ1
	POP	BC
	DJNZ	READNAME
LOOP	CALL	READ1
	CP	1AH
	JR	NZ,BYPASS
	LD	DE,BUFFER1
	CALL	CLOSE
	LD	DE,BUFFER2
	CALL	CLOSE
	JP	DOS
BYPASS	CALL	WRITE2
	LD	B,4
LINE$NO	PUSH	BC
	CALL	READ1
	CALL	WRITE2
	POP	BC
	DJNZ	LINE$NO
	CALL	READ1
	LD	A,89H
	CALL	WRITE2
LOOP2	CALL	READ1
	PUSH	AF
	CALL	WRITE2
	POP	AF
	CP	0DH
	JR	Z,LOOP
	JR	LOOP2
NEXTWORD	INC	HL
	LD	A,(HL)
	CP	21H
	JR	NC,NEXTWORD
	RET
READ1	LD	DE,BUFFER1
	CALL	READBYTE
	RET	Z
	CP	28
	JP	NZ,ERROR
	LD	DE,BUFFER1
	CALL	CLOSE
	LD	DE,BUFFER2
	CALL	CLOSE
	JP	DOS
	RET
WRITE2	LD	DE,BUFFER2
	CALL	WRITBYTE
	JP	NZ,ERROR
	RET
PART1	LD	HL,PM1
PARV01	CALL	MESSAGE
	RET
PART2	LD	HL,PM2
	JR	PARV01
MESSAGE	LD	A,(HL)
	OR	A
	RET	Z
	CALL	0033H
	INC	HL
	CP	0DH
	JR	NZ,MESSAGE
	RET
PM1	DEFM	'CONVERTING '
	DEFB	00H
PM2	DEFM	'TO '
	DEFB	00H
ERROR	EQU	4409H
OPEN$NEW	EQU	4420H
OPEN$EXST	EQU	4424H
CLOSE	EQU	4428H
DEF$EXT	EQU	4473H
READBYTE	EQU	0013H
EXTRACT	EQU	441CH
WRITBYTE	EQU	001BH
DOS	EQU	402DH
DEFEXT1	DEFM	'EDT'
DEFEXT2	DEFM	'FOR'
BUFFER1	DEFS	32
BUFFER2	DEFS	32
BUFFRD1	DEFS	256
BUFFWR2	DEFS	256
	END	CONVERT
