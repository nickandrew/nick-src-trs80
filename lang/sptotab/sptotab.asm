;sptotab: change spaces to tabs. First two fields
;only.
;ver 1.0 on 10-Jul-85.
;
*GET	DOSCALLS
;
	ORG	5300H
START	LD	SP,START
	LD	DE,FCB_IN
	CALL	DOS_EXTRACT
	JP	NZ,DOS_ERROR
	LD	DE,FCB_OUT
	CALL	DOS_EXTRACT
	JP	NZ,DOS_ERROR
	LD	HL,BUFF_IN
	LD	DE,FCB_IN
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
	LD	HL,BUFF_OUT
	LD	DE,FCB_OUT
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,ERROR
;
	XOR	A
	LD	(FIELD),A
;
LOOP	CALL	GET
	JR	NZ,EXIT
LOOP_IT	CP	1AH
	JR	Z,EXIT
	CP	0DH
	JR	Z,C_R
	CP	' '
	JR	Z,SPACE
	CALL	PUT
	JR	LOOP
;
C_R	XOR	A
	LD	(FIELD),A
	LD	A,0DH
	CALL	PUT
	JR	LOOP
;
SPACE	LD	A,(FIELD)
	CP	2
	JR	C,TO_TAB
	LD	A,' '
	CALL	PUT
	JR	LOOP
;
TO_TAB	INC	A
	LD	(FIELD),A
	LD	A,09H
	CALL	PUT
TAB_LP	CALL	GET
	CP	' '
	JR	Z,TAB_LP
	JR	LOOP_IT
;
EXIT	LD	A,1AH
	CALL	PUT
	LD	DE,FCB_OUT
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	LD	DE,FCB_IN
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	JP	DOS
;
PUT	PUSH	DE
	LD	DE,FCB_OUT
	CALL	001BH
	POP	DE
	RET
;
GET	PUSH	DE
	LD	DE,FCB_IN
	CALL	0013H
	POP	DE
	RET
;
FIELD	DEFB	0
;
FCB_IN	DC	32,0
FCB_OUT	DC	32,0
BUFF_IN	DEFS	256
BUFF_OUT DEFS	256
;
	END	START
