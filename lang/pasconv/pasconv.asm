;pasconv: convert pascal-80 source file
;to ascii.
;
*GET	DOSCALLS
CR	EQU	0DH
;
	COM	'<Pasconv 1.1  08-Apr-85>'
	COM	'<Newdos-80, Converts pascal-80 files to Ascii>'
	ORG	5300H
START	LD	SP,START
	LD	A,(HL)
	CP	CR
	JR	NZ,NO_USG
	LD	HL,M_USAGE
	CALL	4467H
	JP	DOS
NO_USG
	LD	DE,PAS_FCB
	CALL	DOS_EXTRACT
	LD	DE,TXT_FCB
	CALL	DOS_EXTRACT
;
	LD	DE,PAS_FCB
	LD	HL,PAS_BUF
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
;
	LD	DE,TXT_FCB
	LD	HL,TXT_BUF
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,DOS_ERROR
;
;Ignore the first 4 bytes
	LD	B,4
IGN	PUSH	BC
	LD	DE,PAS_FCB
	CALL	$GET
	JP	NZ,DOS_ERROR
	POP	BC
	DJNZ	IGN
;
;now put lines into buffer and modify.
	LD	HL,BUFFER
	LD	(TXT_PTR),HL
LOOP	LD	DE,PAS_FCB
	CALL	$GET
	JP	NZ,DOS_ERROR
	OR	A
	JR	Z,END_PAS
	CP	1
	JR	Z,END_LINE
	CP	80H
	JR	NC,EXPAND
	LD	HL,(TXT_PTR)
	LD	(HL),A
	INC	HL
	LD	(TXT_PTR),HL
	JR	LOOP
;
EXPAND	AND	7FH
	LD	B,A
	LD	HL,(TXT_PTR)
EX_1	LD	(HL),' '
	INC	HL
	DJNZ	EX_1
	LD	(TXT_PTR),HL
	JR	LOOP
;
END_LINE
	LD	HL,(TXT_PTR)
	LD	(HL),CR
EL_1	DEC	HL
	LD	A,(HL)
	CP	' '
	JR	Z,EL_1
	INC	HL
	LD	(HL),CR
	LD	HL,BUFFER
	LD	(TXT_PTR),HL
EL_2	PUSH	HL
	LD	A,(HL)
	LD	DE,TXT_FCB
	CALL	$PUT
	JP	NZ,DOS_ERROR
	POP	HL
	LD	A,(HL)
	CP	CR
	JR	Z,LOOP
	INC	HL
	JR	EL_2
;
END_PAS	LD	DE,PAS_FCB
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	LD	DE,TXT_FCB
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	JP	DOS
;
M_USAGE
	DEFM	'Pasconv: Convert Pascal-80 source to Ascii',0AH
	DEFM	'Usage: pasconv infile outfile',CR,0
;
PAS_FCB	DC	32,0
TXT_FCB	DC	32,0
;
PAS_BUF	DC	256,0
TXT_BUF	DC	256,0
;
TXT_PTR	DEFW	0
;
BUFFER	DC	65,0
;
	END	START
