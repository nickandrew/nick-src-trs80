;strip: take line numbers off source file.
;Ver 1.0 on 29-Jun-85.
*GET	DOSCALLS
;
	ORG	5200H
;
START	LD	DE,FCB_IN
	CALL	DOS_EXTRACT
	JP	NZ,DOS_ERROR
	LD	DE,FCB_OUT
	CALL	DOS_EXTRACT
	JP	NZ,DOS_ERROR
	LD	HL,FCB_IB
	LD	DE,FCB_IN
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
	LD	HL,FCB_OB
	LD	DE,FCB_OUT
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,DOS_ERROR
;
SFILE	CALL	GET
	CP	0D3H
	JR	NZ,NUMB_1
	LD	B,6
SNAME	CALL	GET
	JP	NZ,DOS_ERROR
	DJNZ	SNAME
N_LOOP	CALL	GET
	JR	NZ,END_FILE
NUMB_1	AND	7FH
	CALL	IF_NUM
	CALL	Z,BYP_NUM
LOOP_2	PUSH	AF
	CALL	PUT
	JP	NZ,DOS_ERROR
	POP	AF
	CP	0DH
	JR	Z,N_LOOP
	CALL	GET
	JR	NZ,END_FILE
	JR	LOOP_2
;
IF_NUM	CP	'0'
	RET	C	;also NZ.
	CP	'9'+1
	JR	C,IN_1
	OR	A
	RET
IN_1	CP	A
	RET
;
BYP_NUM	CALL	GET
	CALL	IF_NUM
	JR	Z,BYP_NUM
	CALL	GET
	JP	NZ,END_FILE
	RET
;
GET	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	DE,FCB_IN
	CALL	ROM@GET
	POP	HL
	POP	DE
	POP	BC
	RET
;
PUT	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	DE,FCB_OUT
	CALL	ROM@PUT
	POP	HL
	POP	DE
	POP	BC
	RET
;
END_FILE
	CP	1CH
	JR	Z,END_OK
	CP	1DH
	JR	Z,END_OK
	JP	DOS_ERROR
END_OK	LD	DE,FCB_IN
	CALL	DOS_CLOSE
	LD	DE,FCB_OUT
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	JP	DOS_NOERROR
;
FCB_IN	DEFS	32
FCB_OUT	DEFS	32
FCB_IB	DEFS	256
FCB_OB	DEFS	256
;
	END	START
