;DISK2: MODIFIES ASYLUM FOR SAVING
;GAMES TO DISK.
; Assembled OK 30-Mar-85.
	ORG	9267H
	JP	READ
	ORG	0B01CH
	JP	WRITE
	ORG	0D000H
READ	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	REBD
	JR	PAST1
WRITE	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	REBD
	JR	PAST2
REBD	LD	A,(38FFH)
	OR	A
	JR	NZ,REBD
	LD	BC,1800H
	CALL	0060H	;DELAY
	CALL	MESS
N1	CALL	03E3H
	OR	A
	JR	Z,N1
	CP	30H
	JR	C,N1
	CP	3AH
	JR	NC,N1
	PUSH	AF
	LD	C,A
	XOR	A
	CALL	0458H
	LD	BC,4000H
	CALL	0060H
	POP	AF
	SUB	30H
	LD	E,A	;SECTOR NUMBER
	LD	D,25	;TRACK NUMBER
	CALL	START	;START DRIVE
	LD	BC,2000H
	CALL	0060H
	LD	A,0
	CALL	ISSUE
	CALL	TEST0
	LD	(37EEH),DE	;STORE TRACK & SECTOR #S
	LD	A,18H	;FAST SEEK COMMAND
	CALL	ISSUE
	CALL	TEST0
	RET
PAST1	LD	A,88H
	CALL	ISSUE
	POP	HL
	POP	DE
	POP	BC
L3	CALL	TEST1
	LD	A,(37EFH)
	INC	HL
	LD	(HL),A
	DJNZ	L3
	JP	5564H	;RETURN TO MAIN PROGRAM
PAST2	LD	A,0ACH
	CALL	ISSUE
	POP	HL
	POP	DE
	POP	BC
	PUSH	BC
M3	CALL	TEST1
	INC	HL
	LD	A,(HL)
	LD	(37EFH),A
	DJNZ	M3
	POP	BC
	XOR	A
	SUB	B
	LD	B,A
M4	CALL	TEST1
	XOR	A
	LD	(37EFH),A
	DJNZ	M4
	JP	7319H
MESS	LD	HL,MESSAGE
	PUSH	IX
MESV01	LD	A,(HL)
	LD	C,A
	CP	0
	JR	Z,MESV02
	PUSH	HL
	CALL	0458H
	POP	HL
	INC	HL
	JR	MESV01
MESV02	POP	IX
	RET
START	LD	A,1
	LD	(37E1H),A
	RET
DELAY	CALL	START
	LD	B,10
	DJNZ	$
	RET
ISSUE	LD	B,A
	CALL	START
	LD	A,B
	LD	(37ECH),A
	CALL	DELAY
	RET
TEST0	LD	A,(37ECH)
	BIT	0,A
	JR	NZ,TEST0
	RET
TEST1	LD	A,(37ECH)
	BIT	1,A
	JR	Z,TEST1
	RET
MESSAGE	DEFB	1CH
	DEFB	1FH
	DEFM	'PLEASE INSERT DISK0130'
	DEFB	0DH
	DEFB	0DH
	DEFM	'HIT 0-9 FOR SAVE/LOAD SECTOR NUMBER: '
	DEFB	00H
	END	0CE12H
