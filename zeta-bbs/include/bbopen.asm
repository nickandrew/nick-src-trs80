;This code is (possibly) no longer used by Zeta.
;BBOPEN: Allow user-oriented file access..
	COM	'<BBOPEN 1.0b 29-Dec-85>'
;
BB_OPEN
	CALL	SAVE_REG
	CALL	FIX_NAME
	CALL	CHK_INOWN
	JR	NZ,BB_1
	CALL	CHK_IFOWN
	LD	A,19H	;de_denied
	RET	NZ
	CALL	GET_REG
	CALL	DOS_OPEN_EX
	RET
BB_1
	CALL	GET_REG
	CALL	DOS_OPEN_EX
	JR	NZ,BB_2
	CALL	DOS_CLOSE
	LD	A,19H	;de_denied
	OR	A
	RET
BB_2	CP	18H	;de_fnid
	RET	NZ
	LD	A,C
	OR	A
	JR	Z,BB_3
	LD	A,18H
	OR	A
	RET
BB_3	CALL	DOS_OPEN_NEW
	RET	NZ
	CALL	ADD_OWNER
	CALL	GET_REG
	RET	Z
	CALL	DOS_CLOSE
	LD	DE,FCB_OWN
	CALL	DOS_CLOSE
	LD	A,1
	OR	A
	RET
;
SAVE_REG
	LD	(R_HL),HL
	LD	(R_DE),DE
	LD	A,C
	LD	(R_BC),A
	LD	A,B
	LD	(R_BC+1),A
	RET
;
GET_REG
	LD	HL,(R_HL)
	LD	DE,(R_DE)
	LD	A,(R_BC)
	LD	C,A
	LD	A,(R_BC+1)
	LD	B,A
	RET
;
FIX_NAME
	PUSH	DE
FXN_1	LD	A,(DE)
	CP	0DH
	JR	Z,FXN_3
	CP	' '
	JR	Z,FXN_2
	CP	':'
	JR	Z,FXN_2
	INC	DE
	JR	FXN_1
FXN_2	LD	A,0DH
	LD	(DE),A
FXN_3
	POP	DE
	LD	HL,FBUFF
FXN_4	LD	A,(DE)
	LD	(HL),A
;
	CP	'.'
	JR	Z,FXN_6
	CP	0DH
	JR	Z,FXN_6
	CP	'a'
	JR	C,FXN_5
	AND	5FH
	LD	(HL),A
FXN_5
	INC	DE
	INC	HL
	JR	FXN_4
FXN_6	LD	(HL),0DH
	RET
;
ADD_OWNER
	LD	DE,FCB_OWN
	LD	A,(DE)
	BIT	7,A
	JR	NZ,ADD_1
	LD	HL,BUF_OWN
	LD	B,0
	CALL	DOS_OPEN_EX
	RET	NZ
	INC	DE
	LD	A,(DE)
	AND	0F8H
	OR	40H
	LD	(DE),A
	DEC	DE
ADD_1	CALL	DOS_POS_EOF
	RET	NZ
	LD	HL,FBUFF
ADD_2	LD	A,(HL)
	CP	0DH
	JR	Z,ADD_3
	CALL	ROM@PUT
	RET	NZ
	LD	A,(HL)
	INC	HL
	JR	ADD_2
ADD_3	LD	A,' '
	CALL	ROM@PUT
	LD	HL,(USR_NAME)
ADD_4	LD	A,(HL)
	CALL	ROM@PUT
	RET	NZ
	LD	A,(HL)
	INC	HL
	CP	0DH
	JR	NZ,ADD_4
	CALL	DOS_CLOSE
	RET
;
CHK_INOWN
	LD	DE,FCB_OWN
	LD	A,(DE)
	BIT	7,A
	JR	NZ,CIN_1
	LD	HL,BUF_OWN
	LD	B,0
	CALL	DOS_OPEN_EX
	RET	NZ	;BAD. Should be caught
			;explicitly.
	INC	DE
	LD	A,(DE)
	AND	0F8H
	OR	40H
	LD	(DE),A
	DEC	DE
CIN_1
	CALL	DOS_REWIND
CIN_2	LD	HL,FBUFF
CIN_3	CALL	ROM@GET
	RET	NZ
	CP	' '
	JR	Z,CIN_5
	CP	0DH
	JR	Z,CIN_5
	CP	(HL)
	INC	HL
	JR	Z,CIN_3
CIN_4	CP	0DH
	JR	Z,CIN_2
	CALL	ROM@GET
	RET	NZ		;***
	JR	CIN_4
;
CIN_5	LD	A,(HL)
	CP	0DH
	JR	NZ,CIN_4
	RET
;
CHK_IFOWN
	LD	DE,FCB_OWN
CIF_1	LD	HL,(USR_NAME)
CIF_2	CALL	ROM@GET
	RET	NZ		;***
	CP	0DH
	JR	Z,CIF_5
	CP	','
	JR	Z,CIF_4
	CP	(HL)
	INC	HL
	JR	Z,CIF_2
CIF_3	CP	0DH
	JR	Z,RET_NZ
	CP	','
	JR	Z,CIF_1
	CALL	ROM@GET
	RET	NZ		;***
	JR	CIF_3
CIF_4	LD	A,(HL)
	CP	0DH
	JR	NZ,CIF_1
	RET
RET_NZ
	OR	A
	RET	NZ
	CP	1
	RET
;
CIF_5	CP	(HL)
	RET
;
R_BC	DEFW	0
R_DE	DEFW	0
R_HL	DEFW	0
FBUFF	DEFS	32
FCB_OWN
	DEFM	'OWNER/ZMS',0DH
	DC	32-10,0
BUF_OWN	DEFS	256
;
