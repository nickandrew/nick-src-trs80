;Userlist: List members and non-members.
;
*GET	DOSCALLS
*GET	EXTERNAL
*GET	ASCII
;
	ORG	PROG_START
	DEFW	BASE
	DEFW	THIS_PROG_END
	DEFW	TERMINATE
	DEFW	TERMINATE
;End of program load info.
;
	COM	'<Userlist 2.1  04-Dec-88>'
;
	ORG	BASE+100H
START
	LD	SP,START
;
	LD	HL,M_KEYS
	LD	DE,DCB_2O
	CALL	MESS_0
;
	LD	HL,BUFF_IN
	LD	DE,FCB_IN
	LD	B,56
	CALL	DOS_OPEN_EX
	JP	NZ,ERROR
;
	LD	A,(FCB_IN+1)
	AND	0F8H
	LD	(FCB_IN+1),A
;
	XOR	A
	LD	(COUNT),A
;
	CALL	IF_BYPASS
	CALL	READ_1
;
	CALL	PRINT_HDG
;
MEMB_LOOP:
	CALL	IF_BYPASS
	CALL	READ_1
	JR	NZ,MEMB_END
;Check for membership status....
	LD	A,(UF_STATUS)
	BIT	UF_ST_ZERO,A		;if entry unused
	JR	Z,MEMB_LOOP
	BIT	UF_ST_NOTUSER,A
	JR	NZ,MEMB_LOOP		;rude-o people
;;	LD	A,(UF_PRIV2)
;;	BIT	1,A			;visitor bit
;;	JR	NZ,MEMB_LOOP
;
	LD	HL,(UF_UID)
	LD	DE,16
	OR	A
	SBC	HL,DE
	JR	C,MEMB_LOOP		;If uid < 16
	LD	HL,(UF_UID)
	LD	A,H
	AND	L
	CP	0FFH
	JR	Z,MEMB_LOOP		;If uid = FFFF
;
	CALL	PRINT_DATA
	JR	MEMB_LOOP
;
MEMB_END
	XOR	A
	JP	TERMINATE
	LD	HL,M_VIS
	LD	DE,DCB_2O
	CALL	MESS_0
;
	LD	DE,FCB_IN
	CALL	DOS_REWIND
	JP	NZ,ERROR
;
	XOR	A
	LD	(COUNT),A
;
VIS_LOOP
	CALL	IF_BYPASS
	CALL	READ_1
	JR	NZ,VIS_END
	LD	A,(UF_STATUS)
	BIT	UF_ST_ZERO,A
	JR	Z,VIS_LOOP
	BIT	UF_ST_NOTUSER,A
	JR	NZ,VIS_LOOP
	LD	A,(UF_PRIV2)
	BIT	1,A
	JR	Z,VIS_LOOP
	CALL	PRINT_DATA
	JR	VIS_LOOP
;
VIS_END
	LD	A,(PRIV_2)
	BIT	1,A
	JR	NZ,VIS_WHYNOT
	XOR	A
	JP	TERMINATE
;
VIS_WHYNOT
	LD	HL,M_WHYNOT
	LD	DE,DCB_2O
	CALL	MESS_0
	XOR	A
	JP	TERMINATE
;
IF_BYPASS
	LD	A,(COUNT)
	OR	A
	RET	NZ
;
	LD	DE,FCB_IN
	LD	B,0
IFB_1	CALL	ROM@GET
	JP	NZ,ERROR
	DJNZ	IFB_1
	RET
;
READ_1
	LD	HL,US_UBUFF
	LD	DE,FCB_IN
	LD	B,56
READ1_1	CALL	ROM@GET
	JR	NZ,READ1_2
	LD	(HL),A
	INC	HL
	DJNZ	READ1_1
;
	LD	HL,COUNT
	INC	(HL)
	CP	A
	RET
;
READ1_2
	CP	1CH
	JR	Z,READ1_3
	CP	1DH
	JR	Z,READ1_3
	JP	ERROR
READ1_3	CP	1
	RET
;
PRINT_HDG
	LD	HL,M_DATA
	LD	DE,DCB_2O
	CALL	MESS_0
	LD	HL,M_UL
	LD	DE,DCB_2O
	CALL	MESS_0
	RET
;	
PRINT_DATA
	LD	HL,DATA_LINE
	LD	(DATA_POS),HL
;
	LD	HL,UF_NAME
	LD	B,25
	LD	DE,DATA_LINE
PD_01
	LD	A,(HL)
	OR	A
	JR	Z,PD_02
	LD	(DE),A
	INC	HL
	INC	DE
	DJNZ	PD_01
	JR	PD_03
PD_02
	LD	A,' '
	LD	(DE),A
	INC	DE
	DJNZ	PD_02
PD_03
	XOR	A
	LD	(DE),A
	LD	(DATA_POS),DE
	LD	DE,UF_LASTCALL		;Point to D,M,Y
	CALL	DMY_ASCII
	LD	DE,(DATA_POS)
	CALL	STRCAT
	LD	(DATA_POS),DE
;
	LD	HL,(UF_NCALLS)
	LD	DE,TEMP_STR
	CALL	SPRINT_NUMB
;
	LD	DE,TEMP_STR
	CALL	STRLEN
	LD	DE,(DATA_POS)
	LD	A,9
	SUB	L
	LD	B,A
	LD	A,' '
	CALL	STRPAD
	LD	HL,TEMP_STR
	CALL	STRCAT
	LD	B,2
	LD	A,' '
	CALL	STRPAD
;
	LD	A,(UF_PRIV2)
	BIT	IS_VISITOR,A
	JR	NZ,PD_04
	LD	HL,M_MSTAT
	JR	PD_05
PD_04
	LD	HL,M_VSTAT
PD_05
	CALL	STRCAT
;
	LD	HL,DATA_LINE
	LD	DE,DCB_2O
	CALL	MESS_0
	LD	A,CR
	CALL	STD_OUT
	RET
;
ERROR	PUSH	AF
	OR	80H
	CALL	DOS_ERROR
	POP	AF
	JP	TERMINATE
;
STRPAD
	LD	(DE),A
	INC	DE
	DJNZ	STRPAD
	XOR	A
	LD	(DE),A
	RET
;
*GET	TIMES
*GET	ROUTINES
;
COUNT	DEFB	0
;
US_UBUFF			;User data buffer
UF_STATUS	DEFB	0	;Status byte.
UF_NAME		DEFS	24	;Users name
UF_PASSWD	DEFS	13	;Password.
UF_UID		DEFW	0	;User id.
UF_NCALLS	DEFW	0	;Number of logons
UF_LASTCALL	DEFS	3	;Date of last call
UF_PRIV1	DEFB	0	;Priv_1
UF_PRIV2	DEFB	0	;Priv_2
UF_PRIV3	DEFB	0	;Priv_3 (unused)
UF_TDATA	DEFB	0	;Terminal X,Y
UF_REGCOUNT	DEFB	0	;Register info
UF_BADLOGIN	DEFB	0	;Number of bad logons
UF_TFLAG1	DEFB	0	;Flags 1
UF_TFLAG2	DEFB	0	;Flags 2
UF_ERASE	DEFB	0	;Erase char default ^H
UF_KILL		DEFB	0	;Kill  char default ^X
UF_NOTHING	DEFB	0	;Nothing.
;End of UserFile record definitions.
;
;Definitions for UF_STATUS
UF_ST_ZERO	EQU	6	;=1 if record used.
UF_ST_NOTUSER	EQU	5	;1=A fake username or
				;rude disconnection.
;
M_MEMB	DEFM	CR,'List of Members',CR,CR,0
M_VIS	DEFM	CR,CR,'List of Non-members',CR,CR,0
;
M_KEYS	DEFM	CR,CR,CR
	DEFM	'^S to pause, ^Q to resume, <ESC> to abort',CR,0
;
M_DATA	DEFM	'Name                     Last Call  # Calls   Status',CR,0
M_UL	DEFM	'-----------------------  ---------  -------   ------',CR,0
M_WHYNOT
	DEFM	CR,0
M_MSTAT	DEFM	'Member',0
M_VSTAT	DEFM	'Non-member',0
;
DATA_LINE	DEFS	256
DATA_POS	DEFW	0
TEMP_STR	DEFS	8
;
BUFF_IN	DEFS	256
FCB_IN	DEFM	'userfile.zms',CR,0
	DC	32-14,0
THIS_PROG_END	EQU	$
;
	END	START
