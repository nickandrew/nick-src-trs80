;note: Send & Receive short notes.
;
*GET	DOSCALLS.HDR
*GET	EXTERNAL.HDR
*GET	ASCII.HDR
;
	ORG	PROG_START
	DEFW	BASE
	DEFW	THIS_PROG_END
	DEFW	TERMINATE
	DEFW	TERMINATE
;End of program load info.
;
	COM	'<Note 1.0c 20-May-86>'
	ORG	BASE+100H
START	LD	SP,START
	PUSH	HL
;
	LD	HL,BUFF
	LD	DE,FCB_IN
	LD	B,135
	CALL	DOS_OPEN_EX
	JP	NZ,ERROR
;
	LD	A,(FCB_IN+1)
	AND	0F8H
	OR	40H
	LD	(FCB_IN+1),A
;
	POP	HL
;
	LD	A,(HL)
	CP	'-'
	JP	Z,IS_FUNCTION
	CP	CR
	JR	NZ,NO_ASK
;Get a name.
	LD	HL,M_WHOTO
	LD	DE,($STDOUT_DEF)
	CALL	MESS_0
	LD	HL,IN_BUFF
	LD	B,32
	CALL	FORTYHEX
	LD	A,0
	JP	C,EXIT
	LD	A,(HL)
	CP	CR
	LD	A,0
	JP	Z,EXIT
;
NO_ASK
	LD	DE,NAME_BUFF
	LD	B,31
NOTE_01
	LD	A,(HL)
	CP	CR
	JR	Z,NOTE_03
	OR	A
	JR	Z,NOTE_03
	CP	ETX
	JR	Z,NOTE_03
	CP	','
	JR	Z,NOTE_02
;
	LD	(DE),A
	INC	HL
	INC	DE
	DJNZ	NOTE_01
;
	LD	HL,M_NAMELONG
	LD	DE,($STDOUT_DEF)
	CALL	MESS_0
	JP	EXIT
;
NOTE_02	INC	HL
	LD	A,(HL)
	CP	' '
	JR	Z,NOTE_02
NOTE_03
	LD	A,0
	LD	(DE),A
	PUSH	HL
	LD	HL,NAME_BUFF
	CALL	USER_SEARCH
	JR	Z,NOTE_04
;
	POP	HL
	LD	HL,M_NOUSER
	LD	DE,($STDOUT_DEF)
	CALL	MESS_0
	JP	EXIT
;
NOTE_04
	XOR	A
	LD	(FLAGS),A
	LD	HL,(USR_NUMBER)
	LD	(SENDER),HL
	LD	HL,(UF_UID)
	LD	(RECEIVER),HL
;
	LD	HL,(USR_NAME)
	LD	DE,SENDER_NAME
NOTE_04A
	LD	A,(HL)
	LD	(DE),A
	CP	CR
	JR	Z,NOTE_04B
	CP	ETX
	JR	Z,NOTE_04B
	OR	A
	JR	Z,NOTE_04B
	INC	HL
	INC	DE
	JR	NOTE_04A
NOTE_04B
	XOR	A
	LD	(DE),A
;
;Setup date field.
	LD	HL,DATE
	CALL	X_TODAY
	LD	HL,TIME
	CALL	446DH
;
	POP	HL
	LD	A,(HL)
	CP	CR
	JR	Z,ASK
	CP	ETX
	JR	Z,ASK
	OR	A
	JR	Z,ASK
	JR	NO_TEXT_ASK
ASK
	LD	HL,M_TEXT
	LD	DE,($STDOUT_DEF)
	CALL	MESS_0
	LD	HL,IN_BUFF
	LD	B,79
	CALL	FORTYHEX
	JP	C,EXIT
;
NO_TEXT_ASK
	PUSH	HL
	CALL	TERMINATE_S
	POP	HL
	LD	DE,REC_TEXT
	LD	B,79
NOTE_05A
	LD	A,(HL)
	LD	(DE),A
	OR	A
	JR	Z,NOTE_05B
	INC	HL
	INC	DE
	DJNZ	NOTE_05A
NOTE_05B
	XOR	A
	LD	(DE),A
;
	LD	HL,REC_BUFF
	LD	DE,AUX_BUFF
	LD	BC,135
	LDIR
;
NOTE_05
	LD	HL,REC_BUFF
	LD	DE,FCB_IN
	CALL	DOS_READ_SECT
	JR	NZ,NOTE_06
	LD	A,(FLAGS)
	BIT	KILLED,A
	JR	Z,NOTE_05
;
	CALL	DOS_BACK_RECD
	JP	NZ,ERROR
	JR	NOTE_07
;
NOTE_06
	CP	1CH
	JR	Z,NOTE_07
	CP	1DH
	JP	NZ,ERROR
NOTE_07
;
	LD	HL,AUX_BUFF
	LD	DE,REC_BUFF
	LD	BC,135
	LDIR
;
	LD	DE,FCB_IN
	LD	HL,REC_BUFF
	CALL	DOS_WRIT_SECT
	JP	NZ,ERROR
;
	LD	DE,FCB_IN
	CALL	DOS_CLOSE
	JP	NZ,ERROR
	JP	EXIT
;
;
ERROR	PUSH	AF
	OR	80H
	CALL	DOS_ERROR
	POP	AF
	JP	TERMINATE
;
IS_FUNCTION
	INC	HL
	LD	A,(HL)
	CP	'a'
	JR	C,NOTE_08
	CP	'z'+1
	JR	NC,NOTE_08
	AND	5FH
NOTE_08
	CP	'R'
	JR	Z,READ
	CP	'E'
	JR	Z,ERASE
	LD	HL,M_BADCMD
	LD	DE,($STDOUT_DEF)
	CALL	MESS_0
	JP	EXIT
;
EXIT	LD	A,0
	JP	TERMINATE
;
READ
	LD	HL,M_SRCH
	LD	DE,($STDOUT_DEF)
	CALL	MESS_0
	CALL	READ_ALL
	LD	DE,FCB_IN
	CALL	DOS_CLOSE
	JP	NZ,ERROR
	JP	EXIT
;
ERASE
	CALL	READ_ALL
;
	LD	DE,FCB_IN
	CALL	DOS_REWIND
	JP	NZ,ERROR
;
NOTE_09	LD	DE,FCB_IN
	LD	HL,REC_BUFF
	CALL	DOS_READ_SECT
	JP	NZ,NOTE_10
	LD	A,(FLAGS)
	BIT	KILLED,A
	JR	NZ,NOTE_09
	LD	HL,(USR_NUMBER)
	LD	DE,(RECEIVER)
	OR	A
	SBC	HL,DE
	LD	A,H
	OR	L
	JR	NZ,NOTE_09
;
	LD	DE,FCB_IN
	CALL	DOS_BACK_RECD
	JP	NZ,ERROR
;
	LD	A,(FLAGS)
	SET	KILLED,A
	LD	(FLAGS),A
;
	LD	HL,REC_BUFF
	CALL	DOS_WRIT_SECT
	JP	NZ,ERROR
	JR	NOTE_09
;
NOTE_10
	CP	1CH
	JR	Z,NOTE_11
	CP	1DH
	JP	NZ,ERROR
NOTE_11
	LD	DE,FCB_IN
	CALL	DOS_CLOSE
	JP	NZ,ERROR
	JP	EXIT
;
;
READ_ALL
	LD	DE,FCB_IN
	LD	HL,REC_BUFF
	CALL	DOS_READ_SECT
	JP	NZ,NOTE_12
;
	LD	A,(FLAGS)
	BIT	KILLED,A
	JR	NZ,READ_ALL
;
	LD	HL,(USR_NUMBER)
	LD	DE,(RECEIVER)
	OR	A
	SBC	HL,DE
	LD	A,H
	OR	L
	JR	NZ,READ_ALL
;
	LD	HL,M_NOTEFROM
	LD	DE,($STDOUT)
	CALL	MESS_0
;
	LD	HL,SENDER_NAME
	CALL	MESS_0
;
	LD	HL,M_ON
	CALL	MESS_0
;
	LD	HL,REC_DATE
	LD	B,18
NOTE_11A
	LD	A,(HL)
	CALL	$PUT
	INC	HL
	DJNZ	NOTE_11A
;
	LD	A,CR
	CALL	$PUT
;
	LD	HL,REC_TEXT
	CALL	MESS_0
;
	LD	A,CR
	CALL	$PUT
	JR	READ_ALL
;
NOTE_12
	CP	1CH
	JR	Z,NOTE_13
	CP	1DH
	JP	NZ,ERROR
NOTE_13
	LD	A,CR
	LD	DE,($STDOUT)
	CALL	$PUT
	RET
;
*GET	FORTYHEX
*GET	ROUTINES
;
REC_BUFF	;record for buffer.
FLAGS		DEFB	0
;Flag defns
KILLED		EQU	0
NOTE_READ	EQU	1
UNKILLABLE	EQU	2
;
SENDER		DEFW	0
RECEIVER	DEFW	0
SENDER_NAME	DC	32,0
REC_DATE
DATE		DEFM	'dd-mmm-yy '
TIME		DEFM	'hh:mm:ss'
REC_TEXT	DC	80,0
;
FCB_IN		DEFM	'notes.zms',CR
		DC	32-10,0
;
M_SRCH	DEFM	CR,'Looking for notes...',CR,0
;
BUFF		DEFS	256
;
IN_BUFF		DEFS	81
;
NAME_BUFF	DEFS	32
;
M_NAMELONG	DEFM	'Name too long',CR,0
M_NOUSER	DEFM	'Nobody registered by that name',CR,0
M_BADCMD	DEFM	'Usage: NOTE [-r] [-e]',CR,0
M_NOTEFROM	DEFM	CR,'Note from ',0
M_ON		DEFM	', on ',0
M_WHOTO		DEFM	'Send note to which user? ',0
M_TEXT		DEFM	'Enter a single line of text.',CR
		DEFM	': ',0
;
AUX_BUFF	DEFS	135
;
THIS_PROG_END	EQU	$
;
	END	START
