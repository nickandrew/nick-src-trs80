;cmds: List all .cmd files on drive 0
;
*GET	DOSCALLS
*GET	EXTERNAL
*GET	ASCII
;
	ORG	PROG_START
	DEFW	BASE
	DEFW	THIS_PROG_END
	DEFW	TERMINATE
	DEFW	TERMINATE
;End of program load info.
;
	COM	'<cmds 1.0  23-May-87>'
	ORG	BASE+100H
START	LD	SP,START
;
DRIVE	LD	A,CR
	LD	DE,DCB_2O
	CALL	$PUT
	CALL	ONE_DRIVE
;
FINISH
	XOR	A
	JP	TERMINATE
;
ONE_DRIVE
	LD	A,(DRIVE_NO)
	LD	(FCB_DRIVE),A
	LD	DE,FCB_IN
	LD	HL,FCB_DR
	LD	BC,32
	LDIR
	LD	DE,FCB_IN
	LD	HL,BUFF_IN
	LD	B,32
	CALL	DOS_OPEN_EX
	RET	NZ
;
	LD	A,(FCB_IN+1)
	AND	0F8H
	LD	(FCB_IN+1),A
;
	LD	HL,0002H	;rba of 000200H
	LD	C,0
	CALL	DOS_POS_RBA
	JP	NZ,ERROR
;
	XOR	A
	LD	(COLUMN),A
;
LOOP	LD	A,(COLUMN)
	PUSH	AF
	LD	A,(TFLAG2)
	AND	TF_WIDTH
	LD	HL,COLS_TAB
	LD	C,A
	LD	B,0
	ADD	HL,BC
	LD	B,(HL)
	POP	AF
	CP	B
	JR	NZ,LS_01
	XOR	A
	LD	(COLUMN),A
	LD	A,CR
	CALL	STD_OUT
LS_01
	LD	DE,FCB_IN
	LD	HL,SLOT_BUF
	CALL	DOS_READ_SECT
	CP	1CH
	JR	Z,LS_02
	CP	1DH
	JR	Z,LS_02
	OR	A
	JR	Z,LS_03
	CP	6
	JP	NZ,ERROR
	JR	LS_01		;if 6... sector read but
				;record not moved to buff
LS_03	LD	A,(SLOT_FLAGS)
	AND	0D8H		;must be active,
	CP	10H		;non sys,
	JR	NZ,LS_01	;non invisible.
;
	LD	HL,S_CMD
	LD	DE,SLOT_EXT
	LD	B,3
LS_03A	LD	A,(DE)
	CP	(HL)
	JR	NZ,LS_01	;not .CMD
	INC	HL
	INC	DE
	DJNZ	LS_03A
;
	CALL	PRINT_NAME
	LD	HL,COLUMN
	INC	(HL)
	JR	LOOP
;
LS_02	LD	A,CR
	CALL	STD_OUT
	CP	A
	RET
;
PRINT_NAME
	LD	HL,SLOT_NAME
	LD	B,8
	LD	C,0
PN_01	LD	A,(HL)
	CP	' '
	JR	Z,PN_02
	CALL	STD_OUT
	INC	C
	INC	HL
	DJNZ	PN_01
PN_02
;
	JR	PN_04		;Print no extension
	LD	HL,SLOT_EXT
	LD	A,(HL)
	CP	' '
	JR	Z,PN_04
	LD	A,'.'		;Was /
	CALL	STD_OUT
	INC	C
	LD	B,3
PN_03	LD	A,(HL)
	CALL	STD_OUT
	INC	C
	INC	HL
	DJNZ	PN_03
PN_04	LD	A,13	;Field width?
	SUB	C
	LD	B,A
PN_05	LD	A,' '
	CALL	STD_OUT
	DJNZ	PN_05
	RET
;
ERROR	PUSH	AF
	OR	80H
	CALL	DOS_ERROR
	POP	AF
	JP	TERMINATE
;
*GET	ROUTINES
;
COLS_TAB
	DEFB	2,2,4,5		;32,40,64,80
S_CMD	DEFM	'CMD'
DRIVE_NO	DEFB	'0'
FCB_DR	DEFM	'dir.sys:'
FCB_DRIVE	DEFB	'0'
	DEFB	ETX
	DC	32-10,0
FCB_IN	DEFS	32
;
BUFF_IN	DEFS	256
;
SLOT_BUF
SLOT_FLAGS	DEFB	0
SLOT_FILL1	DEFS	4
SLOT_NAME	DEFS	8
SLOT_EXT	DEFS	3
SLOT_FILL2	DEFS	16
;
COLUMN		DEFB	0
;
THIS_PROG_END	EQU	$
;
	END	START
