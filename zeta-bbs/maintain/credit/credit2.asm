;credit2: Other routines for credit
;Last updated: 14-Aug-87
;
_GETYEAR
	LD	A,(4044H)
	LD	L,A
	LD	H,0
	RET
;
_GETMONTH
	LD	A,(4046H)
	LD	L,A
	LD	H,0
	RET
;
_GETDAY
	LD	A,(4045H)
	LD	L,A
	LD	H,0
	RET
;
_CHKSYSOP
	LD	A,(PRIV_1)
	BIT	IS_SYSOP,A
	JR	NZ,$CR2_1
	LD	HL,0
	RET
$CR2_1	LD	HL,1
	RET
;
_UOPEN
	LD	HL,US_BUFF
	LD	DE,US_FCB	;userfile
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,ERROR
	LD	A,(US_FCB+1)
	AND	0F8H
	LD	(US_FCB+1),A
;
	LD	HL,UF_NOTHING
	LD	(_BALPTR),HL
	LD	HL,UF_NAME
	LD	(_UNAME),HL
	LD	HL,UF_PRIV2
	LD	(_PRIV2),HL
	LD	HL,UF_LASTCALL
	LD	(_LASTCALL),HL
	LD	HL,UF_STATUS
	LD	(_STATUS),HL
	RET
;
_UCLOSE
	LD	DE,US_FCB
	CALL	DOS_CLOSE
	JP	NZ,ERROR
	RET
;
_WRITREC
	LD	HL,US_FCB
	PUSH	HL
	CALL	_SETPOS
	POP	HL
;
	LD	DE,US_FCB	;Proper rewrite.
	LD	HL,US_UBUFF	;DATA buffer.
	LD	B,UF_LRL
RWL_1	LD	A,(HL)
	CALL	$PUT
	JP	NZ,ERROR
	INC	HL
	DJNZ	RWL_1
;
	RET
;
ERROR	LD	HL,M_ERROR
	LD	DE,$2
	CALL	MESS_0
	RET
;
M_ERROR	DEFM	'credit: Disk I/O error',CR,0
;
_SKIP
	LD	DE,US_FCB
	LD	B,0
SKIP_1	PUSH	BC
	CALL	$GET
	POP	BC
	DJNZ	SKIP_1
	RET
;
_READREC
	LD	HL,US_FCB
	PUSH	HL
	CALL	_SAVEPOS
	POP	HL
;
	LD	B,UF_LRL
	LD	HL,US_UBUFF
	LD	DE,US_FCB
RR_01	CALL	$GET
	JR	NZ,RR_02
	LD	(HL),A
	INC	HL
	DJNZ	RR_01
	LD	HL,0
	RET
RR_02	LD	HL,1
	RET
;
_SAVEPOS
	LD	HL,2
	ADD	HL,SP
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
;;	LD	HL,FD_FCBPTR
;;	ADD	HL,DE
;;	LD	E,(HL)
;;	INC	HL
;;	LD	D,(HL)
	PUSH	DE
	POP	IX
	LD	A,(IX+5)
	LD	(POSBUF_L),A
	LD	A,(IX+10)
	LD	(POSBUF_M),A
	LD	A,(IX+11)
	LD	(POSBUF_H),A
	LD	HL,0
	RET
;
_SETPOS
	LD	HL,2
	ADD	HL,SP
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
;;	LD	HL,FD_FCBPTR
;;	ADD	HL,DE
;;	LD	E,(HL)
;;	INC	HL
;;	LD	D,(HL)
	LD	A,(POSBUF_L)
	LD	C,A
	LD	A,(POSBUF_M)
	LD	L,A
	LD	A,(POSBUF_H)
	LD	H,A
	CALL	DOS_POS_RBA
	LD	HL,EOF
	RET	NZ
	LD	HL,0
	RET
;
POSBUF_L	DEFB	0
POSBUF_M	DEFB	0
POSBUF_H	DEFB	0
;
_SEARCH
	LD	HL,_NAMESTR
	CALL	USER_SEARCH
	LD	HL,0
	RET	NZ
;
	LD	A,(US_RBA)
	LD	(POSBUF_L),A
	LD	A,(US_RBA+1)
	LD	(POSBUF_M),A
	LD	A,(US_RBA+2)
	LD	(POSBUF_H),A
	LD	HL,1
	RET
;
