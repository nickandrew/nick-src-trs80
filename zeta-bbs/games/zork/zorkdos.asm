;ZorkDos: Routines to let ZORK run under dos.
;
OPEN_FILE
	LD	DE,ZFCB
	LD	HL,ZBUFF
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
	RET
;
READ_FILE
	LD	(H42F0),HL
	LD	(H584E),A
	LD	L,A
	LD	H,0
	ADD	HL,HL
	PUSH	HL
	POP	BC
	LD	DE,ZFCB
	CALL	DOS_POSIT
	JP	NZ,DOS_ERROR
	CALL	DOS_READ_SECT
	JP	NZ,DOS_ERROR
	LD	DE,(H42F0)
	LD	HL,ZBUFF
	LD	BC,100H
	LDIR
	LD	DE,ZFCB
	CALL	DOS_READ_SECT
	JP	NZ,DOS_ERROR
	LD	DE,(H42F0)
	INC	D
	LD	HL,ZBUFF
	LD	BC,100H
	LDIR
	LD	HL,(H42F0)
	XOR	A
	LD	DE,200H
RF_1	ADD	A,(HL)
	LD	B,A
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	LD	A,B
	JR	NZ,RF_1
	LD	A,(H584E)
	LD	HL,H5977
	CALL	ADDHLA
	LD	A,B
	CP	(HL)
	RET	Z
	LD	A,1
	JP	DOS_ERROR
;
ZFCB	DEFM	'Zork1/dat.gue1',0DH
	DC	32-15,0
ZBUFF	DEFS	256
;
NEW_VDU	;entry C=character.
	LD	A,C
	CP	0AH
	JR	Z,VDU_EXIT
	CP	' '
	JR	Z,NV_SPC
	CP	0DH
	JR	Z,NV_CR
	CALL	ROM@PUT_VDU
VDU_EXIT
	POP	HL
	POP	DE
	POP	BC
	RET
NV_SPC
	LD	A,(4020H)
	AND	3FH
	CP	34H
	JR	NC,NV_WRAP
	LD	A,' '
	CALL	ROM@PUT_VDU
	JR	VDU_EXIT
NV_WRAP
NV_CR
	LD	A,0DH
	CALL	ROM@PUT_VDU
	CALL	NEW_LINE
	JR	VDU_EXIT
;
