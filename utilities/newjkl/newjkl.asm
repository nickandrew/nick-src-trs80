;NEWJKL/EDT: GRAPHIC SCREEN-DUMP ROUTINE FOR
;NEWDOS/80 VERSION 2.03 (SYS15/SYS & MOD. SYS1/SYS).
	ORG	4D00H
DUMP	CALL	PTRSET
	XOR	A	;LINE 0 TO 15
	LD	(LINE),A
LINEX	CALL	GRAFIX
	LD	HL,LINE
	INC	(HL)
	LD	A,(HL)
	CP	10H
	JR	NZ,LINEX
	LD	A,1BH
	CALL	PTROUT
	LD	A,40H
	CALL	PTROUT
	RET
PTRSET	LD	HL,SETTXT
	LD	B,6
PTRV01	LD	A,(HL)
	INC	HL
	CALL	PTROUT
	DJNZ	PTRV01
	RET
PTROUT	PUSH	AF
	PUSH	BC
	LD	B,A
	IN	A,(253)
	AND	060H
	CP	20H
	LD	A,08H
	JP	NZ,4409H
PTRV11	IN	A,(253)
	AND	0E0H
	CP	20H
	JR	NZ,PTRV11
	LD	A,B
	OUT	(253),A
	POP	BC
	POP	AF
	RET
PRINT3	PUSH	BC
	LD	B,3
PRIV01	CALL	PTROUT
	DJNZ	PRIV01
	POP	BC
	RET
SETGRX	LD	A,1BH
	CALL	PTROUT
	LD	A,'K'
	CALL	PTROUT
	LD	A,80H
	CALL	PTROUT
	LD	A,1
	CALL	PTROUT
	RET
GETADD	LD	L,0
	LD	A,(LINE)
	SRL	A
	RR	L
	SRL	A
	RR	L
	ADD	A,3CH
	LD	H,A
	LD	A,(CHRPOS)
	ADD	A,L
	LD	L,A
	RET
GRAFIX	XOR	A
	LD	(INLINE),A
GRALP	XOR	A
	LD	(CHRPOS),A
	LD	A,0DH
	CALL	PTROUT
	CALL	SETGRX
GRAV01	CALL	GETADD
	LD	A,(HL)
	CP	20H
	JR	NC,GRAV02
	LD	A,'.'
GRAV02	LD	B,A
	CP	80H
	JR	NC,GRX
	LD	A,(INLINE)
	CP	0
	JR	Z,GRAV10
	LD	A,B
	CP	60H
	JR	NC,GRAV04
	XOR	A
	LD	B,6
GRAV03	CALL	PTROUT
	DJNZ	GRAV03
	JR	NXTCHR
GRAV04	SUB	60H
	LD	B,A
	ADD	A,A
	ADD	A,B
	ADD	A,A
	LD	E,A
	LD	D,0
	LD	HL,DESTBL
	ADD	HL,DE
	LD	B,6
GRAV05	LD	A,(HL)
	INC	HL
	CALL	PTROUT
	DJNZ	GRAV05
	JR	NXTCHR
GRAV10	LD	A,B
	SUB	20H
	LD	L,A
	LD	H,0
	LD	E,L
	LD	D,H
	ADD	HL,HL
	ADD	HL,DE
	ADD	HL,HL
	LD	DE,LETTBL
	ADD	HL,DE
	LD	B,6
GRAV11	LD	A,(HL)
	INC	HL
	CALL	PTROUT
	DJNZ	GRAV11
	JR	NXTCHR
GRX	AND	03FH
	LD	B,A
	LD	A,(INLINE)
	CP	0
	JR	Z,GRXV10
	XOR	A
	BIT	4,B
	JR	Z,GRXV01
	OR	0F0H
GRXV01	CALL	PRINT3
	XOR	A
	BIT	5,B
	JR	Z,GRXV03
	OR	0F0H
GRXV03	CALL	PRINT3
	JR	NXTCHR
GRXV10	XOR	A
	BIT	0,B
	JR	Z,GRXV11
	OR	0F0H
GRXV11	BIT	2,B
	JR	Z,GRXV12
	OR	0FH
GRXV12	CALL	PRINT3
	XOR	A
	BIT	1,B
	JR	Z,GRXV13
	OR	0F0H
GRXV13	BIT	3,B
	JR	Z,GRXV14
	OR	0FH
GRXV14	CALL	PRINT3
NXTCHR	LD	A,(CHRPOS)
	INC	A
	LD	(CHRPOS),A
	CP	64
	JP	NZ,GRAV01
	LD	A,(INLINE)
	CP	1
	RET	Z
	INC	A
	LD	(INLINE),A
	LD	A,0DH
	CALL	PTROUT
	JP	GRALP
LINE	DEFB	0
INLINE	DEFB	0
CHRPOS	DEFB	0
SETTXT	DEFB	1BH
	DEFB	40H
	DEFB	0DH
	DEFB	1BH
	DEFB	33H
	DEFB	10
;END OF SETTXT
LETTBL	EQU	4F00H
DESTBL	EQU	5140H
	END	DUMP
