;split32k:	Split one file into 32-k subfiles.
;
*GET	DOSCALLS
;
SIZEK	EQU	32
SIZEB	EQU	SIZEK*1024
SIZES	EQU	SIZEK*4
	COM	'<Split32k 1.0  27-Dec-88>'
	ORG	5300H
START
	LD	SP,START
	LD	DE,IN_FCB
	CALL	DOS_EXTRACT
	JP	NZ,ERROR
;
	LD	HL,IN_BUF
	LD	DE,IN_FCB
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,ERROR
;
LOOP_1
	LD	HL,DATA_BUFF
	LD	(BUFF_POS),HL
	LD	B,128		;32k
LOOP_2
	PUSH	BC
	LD	DE,IN_FCB
	CALL	DOS_READ_SECT
	JP	NZ,EOF
	LD	HL,IN_BUF
	LD	DE,(BUFF_POS)
	LD	BC,256
	LDIR
	LD	(BUFF_POS),DE
	POP	BC
	DJNZ	LOOP_2
	CALL	FLUSH_OUT
	JR	LOOP_1
;
EOF
	POP	BC
	LD	A,B
	CP	128
	CALL	NZ,FLUSH_OUT
	XOR	A
	JP	DOS_NOERROR
;
FLUSH_OUT
	LD	A,128
	SUB	B
	LD	B,A
	PUSH	BC
;
	LD	A,(OUT_NUM)
	INC	A
	LD	(OUT_NUM),A
;
	LD	HL,OUT_BUF
	LD	DE,OUT_FCB
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,ERROR
;
	LD	HL,DATA_BUFF
	LD	(BUFF_POS),HL
	POP	BC
LOOP_3
	PUSH	BC
	LD	HL,(BUFF_POS)
	LD	DE,OUT_BUF
	LD	BC,256
	LDIR
	LD	(BUFF_POS),HL
;
	LD	DE,OUT_FCB
	CALL	DOS_WRIT_SECT
	JP	NZ,ERROR
;
	POP	BC
	DJNZ	LOOP_3
;
	LD	DE,OUT_FCB
	CALL	DOS_CLOSE
	RET
;
ERROR
	PUSH	AF
	OR	80H
	CALL	DOS_ERROR
	POP	AF
	JP	DOS_NOERROR
;
;
;
BUFF_POS	DEFW	0
;
OUT_FCB		DEFM	'output'
OUT_NUM		DEFM	'@'
		DEFM	13
		DC	32-8,0
;
;
IN_FCB	DEFS	32
IN_BUF	DEFS	256
OUT_BUF	DEFS	256
;
DATA_BUFF	DEFS	32768
;
	END	START
