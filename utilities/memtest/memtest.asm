;Memtest/asm: background routine to test memory.
;Will display a warning message if memory found
;to be bad!
; Version 1.0 on 21-Oct-84.
;
*GET	DOSCALLS
ENQUEUE	EQU	4410H
;
	ORG	5200H
START	LD	HL,(HIMEM)
	LD	BC,PROGEND-PROGST
	OR	A
	SBC	HL,BC
	LD	(HIMEM),HL
	INC	HL
	PUSH	HL
	CALL	RELOCATE
	EX	DE,HL
	LD	HL,PROGST
	LDIR
	POP	DE
	CALL	ENQUEUE
	LD	HL,SIGNON
	CALL	4467H	;Print msg.
	JP	DOS_NOERROR
;
RELOCATE	PUSH	HL
	EX	DE,HL
	LD	HL,(RELOC0)
	ADD	HL,DE
	LD	(RELOC0),HL
	LD	HL,(RELOC1)
	ADD	HL,DE
	LD	(RELOC1),HL
	LD	HL,(RELOC2)
	ADD	HL,DE
	LD	(RELOC2),HL
	LD	HL,(RELOC3)
	ADD	HL,DE
	LD	(RELOC3),HL
	LD	HL,(RELOC4)
	ADD	HL,DE
	LD	(RELOC4),HL
	LD	HL,(RELOC5)
	ADD	HL,DE
	LD	(RELOC5),HL
	LD	HL,(RELOC6)
	ADD	HL,DE
	LD	(RELOC6),HL
	LD	HL,(RELOC7)
	ADD	HL,DE
	LD	(RELOC7),HL
	LD	HL,(RELOC8)
	ADD	HL,DE
	LD	(RELOC8),HL
	POP	HL
	RET
;
SIGNON	DEFM	'Memtest: Relocatable memory tester',0AH
	DEFM	'Version 1.0 21-Oct-84',0AH
	DEFM	'Warns when memory fault occurs',0DH
;
;Relocatable code starts here.
PROGST	DEFW	0000H		;fwd chain addr.
	DEFB	100		;do every 2.5 secs.
	DEFB	100
	LD	A,(3C3FH)
	LD	(SAVED-PROGST),A
RELOC0	EQU	$-2
	LD	A,0BFH		;Graphic block.
	LD	(3C3FH),A	;Show I'm alive.
	LD	A,(BADFLAG-PROGST)
RELOC1	EQU	$-2
	OR	A
	JR	NZ,MEMERR
	LD	DE,REGION1-PROGST
RELOC2	EQU	$-2
	LD	HL,8000H
	CALL	CALL1-PROGST
RELOC3	EQU	$-2
	LD	HL,0C000H
	INC	DE
	CALL	CALL1-PROGST
RELOC4	EQU	$-2
	LD	A,(DE)
	DEC	DE
	LD	C,A
	LD	A,(DE)
	OR	C
	LD	(BADFLAG-PROGST),A
RELOC5	EQU	$-2
	LD	A,(SAVED-PROGST)
RELOC6	EQU	$-2
	LD	(3C3FH),A
	RET
;
CALL1	XOR	A
	LD	(DE),A
	LD	B,0
LOOP1	LD	C,(HL)
	LD	(HL),B
	LD	A,(HL)
	LD	(HL),C
	SUB	B
	LD	C,A
	LD	A,(DE)
	OR	C
	LD	(DE),A
	DJNZ	LOOP1
	RET
;
MEMERR	LD	HL,3C00H
	LD	BC,40H
	LD	DE,TOPLINE-PROGST
RELOC7	EQU	$-2
	PUSH	DE
	LDIR
	LD	B,5
LOOP2	PUSH	BC
	LD	HL,MESSAG-PROGST
RELOC8	EQU	$-2
	LD	DE,3C00H
	LD	BC,40H
	LDIR
	LD	BC,8000H
	CALL	60H
	LD	HL,3C00H
	LD	DE,3C01H
	LD	(HL),' '
	LD	BC,3FH
	LDIR
	LD	BC,8000H
	CALL	60H
	POP	BC
	DJNZ	LOOP2
	POP	HL
	LD	DE,3C00H
	LD	BC,40H
	LDIR
	RET
;
TOPLINE	DEFS	64	;Top saved screen line.
MESSAG	DEFM	'****** WARNING: Memory is no longer reliable!! ******           '
;
SAVED	DEFB	0
BADFLAG	DEFB	0
REGION1	DEFB	0
REGION2	DEFB	0
PROGEND	NOP
;
	END	START
