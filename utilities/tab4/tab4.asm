;Tab4:   Change tabs in source file to spaces.
;          Default tab width is 4.
;
*GET	DOSCALLS
CR	EQU	0DH
LF	EQU	0AH
TAB	EQU	09H
;
	COM	'<Tab4 15-Mar-87>'
;
	ORG	5300H
START	LD	SP,START
	LD	DE,FCB_I
	CALL	DOS_EXTRACT
	LD	DE,FCB_O
	CALL	DOS_EXTRACT
	LD	HL,BUF_I
	LD	DE,FCB_I
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
;
	LD	HL,BUF_O
	LD	DE,FCB_O
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,DOS_ERROR
;
	LD	A,1
	LD	(POS),A
;
LOOP	LD	DE,FCB_I
	CALL	$GET
	JR	NZ,END_OF_FILE
	CP	TAB
	JR	NZ,CHK_CR
;
	LD	A,(POS)
	DEC	A
	AND	3
	LD	B,A
	LD	A,4
	SUB	B
	LD	B,A
	LD	DE,FCB_O
TAB_LP	PUSH	BC
	LD	A,' '
	CALL	$PUT
	JP	NZ,ERROR
	POP	BC
	DJNZ	TAB_LP
;
	LD	A,(POS)
	ADD	A,4
	AND	0FCH
	ADD	A,1
	LD	(POS),A
	JR	LOOP
;
CHK_CR	CP	CR
	JR	Z,IS_CR
	CP	LF
	JR	Z,IS_CR		;LF same as CR.
	JR	PUTCHAR
IS_CR	XOR	A
	LD	(POS),A
	LD	A,CR
PUTCHAR
	LD	DE,FCB_O
	CALL	$PUT
	JP	NZ,ERROR
	LD	A,(POS)
	INC	A
	LD	(POS),A
	JR	LOOP
;
END_OF_FILE
	CP	1CH
	JR	Z,EOF_1
ERROR	PUSH	AF
	OR	80H
	CALL	DOS_ERROR
	POP	AF
	JP	DOS_NOERROR
;
EOF_1
	LD	DE,FCB_I
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	LD	DE,FCB_O
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	JP	DOS_NOERROR
;
POS	DEFB	0
FCB_I	DEFS	32
FCB_O	DEFS	32
BUF_I	DEFS	256
BUF_O	DEFS	256
;
	END	START
