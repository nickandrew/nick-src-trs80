;uncmprs/asm: Expand space compression codes in a file
;and delete unusual characters.
;Version 1.0 on 12-May-86
;
*GET	DOSCALLS
;
	COM	'<Uncompress utility, 12-May-86>'
	ORG	5200H
START	LD	DE,FCB_IN
	CALL	DOS_EXTRACT
	JP	NZ,DOS_ERROR
	LD	DE,FCB_OUT
	CALL	DOS_EXTRACT
	JP	NZ,DOS_ERROR
;
	LD	HL,BUFF_IN
	LD	DE,FCB_IN
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
;
	LD	HL,BUFF_OUT
	LD	DE,FCB_OUT
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,DOS_ERROR
;
LOOP	LD	DE,FCB_IN
	CALL	$GET
	JR	Z,CHAROK
	CP	1CH
	JR	Z,EOF
	CP	1DH
	JP	NZ,DOS_ERROR
EOF	LD	DE,FCB_OUT
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	JP	DOS
;
CHAROK	CP	' '
	JR	C,CONTROL
	CP	80H
	JR	NC,GRAPHICS
OUTPUT	LD	DE,FCB_OUT
	CALL	$PUT
	JP	NZ,DOS_ERROR
	JR	LOOP
;
CONTROL	CP	0DH
	JR	Z,OUTPUT
	CP	9
	JR	Z,OUTPUT
	JR	LOOP
;
GRAPHICS
	CP	0C0H
	JR	C,LOOP
	SUB	0C0H
	LD	B,A
GLOOP	PUSH	BC
	LD	A,' '
	LD	DE,FCB_OUT
	CALL	$PUT
	JP	NZ,DOS_ERROR
	POP	BC
	DJNZ	GLOOP
	JR	LOOP
;
FCB_IN	DEFS	32
FCB_OUT	DEFS	32
BUFF_IN	DEFS	256
BUFF_OUT	DEFS	256
;
	END	START
