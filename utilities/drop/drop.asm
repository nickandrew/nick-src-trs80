;drop/asm: convert a file to lower case.
; Ver 1.0 on 31-Dec-84.
;
*GET	DOSCALLS
;
	ORG	5200H
START	LD	DE,FCB_IN
	CALL	DOS_EXTRACT
	LD	DE,FCB_OUT
	CALL	DOS_EXTRACT
	LD	DE,FCB_IN
	LD	HL,BUF_IN
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,DOS_ERROR
	LD	DE,FCB_OUT
	LD	HL,BUF_OUT
	LD	B,0
	CALL	DOS_OPEN_NEW
	JP	NZ,DOS_ERROR
LOOP	LD	DE,FCB_IN
	CALL	$GET
	JP	NZ,DOS_ERROR
	CALL	DROP
	PUSH	AF
	LD	DE,FCB_OUT
	CALL	$PUT
	JP	NZ,DOS_ERROR
	POP	AF
	OR	A
	JR	NZ,LOOP
	LD	DE,FCB_OUT
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	LD	DE,FCB_IN
	CALL	DOS_CLOSE
	JP	NZ,DOS_ERROR
	JP	DOS_NOERROR
;
DROP	CP	'A'
	RET	C
	CP	'Z'+1
	RET	NC
	ADD	A,'a'-'A'
	RET
;
FCB_IN	DEFS	32
FCB_OUT	DEFS	32
BUF_IN	DEFS	256
BUF_OUT	DEFS	256
;
	END	START
