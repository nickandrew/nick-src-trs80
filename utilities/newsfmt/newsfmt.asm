;newsfmt: Reformat news files to suit.
;
*GET	DOSCALLS
*GET	ASCII
;
	COM	'<Newsfmt 1.0a 07-Jun-87>'
	ORG	5300H
START	LD	SP,START
	LD	A,(HL)
	CP	CR
	JP	Z,USAGE
	OR	A
	JP	Z,USAGE
	LD	(ARG),HL
FLOOP
	LD	HL,(ARG)
	LD	A,(HL)
	OR	A
	JP	Z,FINI
	CP	CR
	JP	Z,FINI
	LD	DE,FCB_IN
	CALL	DOS_EXTRACT
	JP	NZ,ERROR
;
	LD	(ARG),HL
;
	LD	HL,FCB_IN
	LD	DE,FCB_OUT
	LD	BC,32
	LDIR
;
	LD	HL,BUFF_IN
	LD	DE,FCB_IN
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,ERROR
;
	LD	HL,BUFF_OUT
	LD	DE,FCB_OUT
	LD	B,0
	CALL	DOS_OPEN_EX
	JP	NZ,ERROR
;
;First read and delete unnecessary keywords.
K_LOOP	CALL	READLINE
	JR	NZ,EOF
	LD	A,(LINE)
	CP	NL
	JR	Z,K_END
;
	CALL	KEY_SRCH
	JR	Z,K_WRITE
;Bypass rest of any long lines.
K_01	LD	A,(PARTLINE)
	OR	A
	JR	Z,K_LOOP
	CALL	READLINE
	JR	K_01
;
K_WRITE
	CALL	WRITELINE
	LD	A,(PARTLINE)
	OR	A
	JR	Z,K_LOOP
	CALL	READLINE
	JR	NZ,K_END
	JR	K_WRITE
;
K_END	LD	HL,L_GATED
	CALL	LINECOPY
	CALL	WRITELINE
;
LOOP	CALL	READLINE
	JR	NZ,EOF
	CALL	WRITELINE
	JR	LOOP
;
EOF	;; CALL	OFLUSH
	LD	DE,FCB_OUT
	CALL	DOS_CLOSE
	JP	NZ,ERROR
	JP	FLOOP
;
FINI
	JP	DOS_NOERROR
;
USAGE	LD	HL,M_USAGE
	CALL	4467H
	JP	DOS_NOERROR
;
ERROR	PUSH	AF
	OR	80H
	CALL	DOS_ERROR
	POP	AF
	JP	DOS_NOERROR
;
KEY_SRCH
	LD	DE,K_FROM
	CALL	KEY_CMP
	RET	Z
	LD	DE,K_NEWS
	CALL	KEY_CMP
	RET	Z
	LD	DE,K_SUBJ
	CALL	KEY_CMP
	JR	Z,KEY_SUBJ
	LD	DE,K_ID
	CALL	KEY_CMP
	RET	Z
	LD	DE,K_DATE
	CALL	KEY_CMP
	RET	Z
	LD	DE,K_LINES
	CALL	KEY_CMP
	RET	Z
	LD	DE,K_KEYW
	CALL	KEY_CMP
	RET	Z
	LD	DE,K_REPLY
	CALL	KEY_CMP
	RET	Z
	RET
;
;Write the subject line to a summary file
KEY_SUBJ
;
	LD	HL,LINE
KS_01	LD	A,(HL)
	OR	A
	JR	Z,KS_02
	CP	NL
	JR	Z,KS_02
	CALL	ROM@PUT_VDU
	INC	HL
	JR	KS_01
KS_02	LD	A,CR
	CALL	ROM@PUT_VDU
	CP	A
	RET
;
KEY_CMP
	LD	HL,LINE
KC_01	LD	A,(DE)
	OR	A
	RET	Z
	CP	(HL)		;To: Call CI_CMP
	RET	NZ
	INC	HL
	INC	DE
	JR	KC_01
;
READLINE
	LD	HL,LINE
	LD	B,80
	XOR	A
	LD	(PARTLINE),A
RL_01	CALL	GETB
	RET	NZ
	CP	1AH
	JR	Z,RL_03
	LD	(HL),A
	INC	HL
	CP	NL
	JR	Z,RL_02
	DJNZ	RL_01
	LD	A,1
	LD	(PARTLINE),A
RL_02	LD	(HL),0
	CP	A
	RET
RL_03	LD	(HL),0
	OR	A
	RET
;
WRITELINE
	LD	HL,LINE
WL_01	LD	A,(HL)
	OR	A
	RET	Z
	CP	NL
	JR	NZ,WL_02
	LD	A,CR
WL_02	CALL	PUTB
	INC	HL
	JR	WL_01
;
LINECOPY
	LD	DE,LINE
LC_01	LD	A,(HL)
	LD	(DE),A
	OR	A
	RET	Z
	INC	HL
	INC	DE
	JR	LC_01
;
GETB	PUSH	DE
	LD	DE,FCB_IN
	CALL	ROM@GET
	POP	DE
	RET
;
PUTB	PUSH	DE
	LD	DE,FCB_OUT
	CALL	ROM@PUT
	JP	NZ,ERROR
	POP	DE
	RET
;
K_FROM	DEFM	'From: ',0
K_NEWS	DEFM	'Newsgroups: ',0
K_SUBJ	DEFM	'Subject: ',0
K_ID	DEFM	'Message-ID: ',0
K_DATE	DEFM	'Date: ',0
K_LINES	DEFM	'Lines: ',0
K_KEYW	DEFM	'Keywords: ',0
K_REPLY	DEFM	'Reply-To: ',0
;
LINE	DEFS	81
;
L_GATED
	DEFM	'Gated-By: all@zeta  (Fidonet [712/602])',NL,NL,0
M_USAGE
	DEFM	'Usage: Newsfmt filename',CR,0
;
PARTLINE	DEFB	0
ARG	DEFW	0
;
FCB_IN	DEFS	32
FCB_OUT	DEFS	32
BUFF_IN	DEFS	256
BUFF_OUT	DEFS	256
;
	END	START
