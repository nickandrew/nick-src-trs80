;remote : Source for remote driver.
;Last updated : 24-Jan-88
;

*GET	DOSCALLS

	ORG	0FF00H
NEW_DO
	DC	12,0		;For device linking
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,C
	CALL	DO_OUT
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET	
NEW_KI
	DC	12,0
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	KI_IN
	POP	HL
	POP	DE
	POP	BC
	RET	
;
KI_IN
	CALL	TTY_IN
	RET
;
TTY_IN	IN	A,(0F9H)
	BIT	01H,A
	LD	A,0
	JR	NZ,AAJ
	RET	
AAJ	IN	A,(0F8H)
	OR	A
	RET	
;
DO_OUT
	CP	' '
	JR	NC,DO_1
	CP	0DH
	RET	NZ
	CALL	TTY_OUT
	LD	A,0AH
	CALL	TTY_OUT
	RET
DO_1	CALL	TTY_OUT
	RET
;
TTY_OUT	PUSH	AF
AAL	IN	A,(0F9H)
	BIT	00H,A
	JR	Z,AAL
	POP	AF
	OUT	(0F8H),A
	RET	
;
START	LD	A,082H
	OUT	(0F9H),A
	LD	A,40H
	OUT	(0F9H),A
	LD	A,0EH
	OUT	(0F9H),A
	LD	A,025H
	OUT	(0F9H),A
	JP	DOS_NOERROR
;
	END	START
